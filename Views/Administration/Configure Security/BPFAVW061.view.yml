Web View:
  Name:               BPFSecurityIPMonitoringSettingsView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal Security IP Bans Settings View"

  - JPN:
      Description:        "Portal Security IP Bans Settings View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Componenttag('BPFAPP005') Verticalscroll(True)
    * Reference Application Objects
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFSecurityIPMonitoringSettingsViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFSecurityDataClient) Name(#SecuritySettings) Reference(*DYNAMIC)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(110) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#MainLayout)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#EnabledControlPanel) Parent(#MainLayout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem4) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#CleanupControlPanel) Parent(#MainLayout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(24)
    
    
    * Enabled Panel Control Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem5) Manage(#ViewIPBansAction) Parent(#MainLayout) Row(#LayoutRow2) Sizing(FitToWidth) Column(#LayoutColumn3) Alignment(TopLeft) Flow(Down) Margintop(24) Marginbottom(24)
    
    Define_Com Class(#PRIM_TBLO) Name(#EnabledControlPanelLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#EnabledControlPanelLayout1Row1) Displayposition(1) Parent(#EnabledControlPanelLayout) Units(Content) Height(199)
    Define_Com Class(#PRIM_TBLO.Row) Name(#EnabledControlPanelLayout1Row2) Displayposition(2) Parent(#EnabledControlPanelLayout) Units(Pixels) Height(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#EnabledControlPanelColumn1) Displayposition(1) Parent(#EnabledControlPanelLayout) Minwidth(44)
    Define_Com Class(#PRIM_TBLO.Column) Name(#EnabledControlPanelColumn2) Displayposition(2) Parent(#EnabledControlPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#EnabledControlPanelColumn3) Displayposition(3) Parent(#EnabledControlPanelLayout) Minwidth(44)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#EnabledControlPanelLayoutItem1) Alignment(TopLeft) Column(#EnabledControlPanelColumn2) Manage(#EnableIPMonitoring) Parent(#EnabledControlPanelLayout) Row(#EnabledControlPanelLayout1Row1) Sizing(FitToWidth) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#EnabledControlPanelLayoutItem2) Alignment(TopLeft) Column(#EnabledControlPanelColumn2) Manage(#EnabledControlPanelHeading) Parent(#EnabledControlPanelLayout) Row(#EnabledControlPanelLayout1Row1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24)
    
    * Enabled Panel Control Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#EnabledControlPanelLayoutItem3) Manage(#InvalidAttemptsDropdown) Parent(#EnabledControlPanelLayout) Row(#EnabledControlPanelLayout1Row1) Sizing(FitToWidth) Column(#EnabledControlPanelColumn2) Alignment(TopLeft) Flow(Down) Margintop(16)
    
    Define_Com Class(#PRIM_TBLO) Name(#CleanupControlPanelLayout) Sizing(ContentHeight)
    
    
    * Controls
    Define_Com Class(#PRIM_TBLO.Column) Name(#CleanupControlPanelLayoutColumn1) Displayposition(1) Parent(#CleanupControlPanelLayout) Minwidth(44)
    Define_Com Class(#PRIM_TBLO.Column) Name(#CleanupControlPanelLayoutColumn2) Displayposition(2) Parent(#CleanupControlPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#CleanupControlPanelLayoutColumn3) Displayposition(3) Parent(#CleanupControlPanelLayout) Minwidth(44)
    Define_Com Class(#PRIM_TBLO.Row) Name(#CleanupControlPanelLayoutRow1) Displayposition(1) Parent(#CleanupControlPanelLayout) Height(309) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#CleanupControlPanelLayoutRow2) Displayposition(2) Parent(#CleanupControlPanelLayout) Height(24) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CleanupControlPanelLayoutItem1) Manage(#CleanupControlPanelHeading) Parent(#CleanupControlPanelLayout) Row(#CleanupControlPanelLayoutRow1) Sizing(ContentWidthAndHeight) Column(#CleanupControlPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CleanupControlPanelLayoutItem2) Alignment(TopLeft) Column(#CleanupControlPanelLayoutColumn2) Manage(#BanClearServiceToggle) Parent(#CleanupControlPanelLayout) Row(#CleanupControlPanelLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CleanupControlPanelLayoutItem3) Manage(#BanDurationDropdown) Parent(#CleanupControlPanelLayout) Row(#CleanupControlPanelLayoutRow1) Sizing(FitToWidth) Column(#CleanupControlPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CleanupControlPanelLayoutItem4) Manage(#CheckIntervalDropdown) Parent(#CleanupControlPanelLayout) Row(#CleanupControlPanelLayoutRow1) Sizing(FitToWidth) Column(#CleanupControlPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CleanupControlPanelLayoutItem5) Manage(#CurrentValueWarningText) Parent(#CleanupControlPanelLayout) Row(#CleanupControlPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#CleanupControlPanelLayoutColumn2) Alignment(TopLeft) Flow(Down)
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Displayposition(1) Left(341) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Style(#NavigationLinkStyle) Caption('IP Address Monitoring')
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(2) Height(55) Left(341) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('The portal can monitor IP addresses for suspicious request behaviour. You can are able to enabled monitoring fo IP addresses and set a limit on the number of requests from an IP that appear to be invalid. A background server process can also be controlled and configured to check if IP addresses that have been banned and clear is ban after a defined amount of time.')
    
    Define_Com Class(#PRIM_PANL) Name(#EnabledControlPanel) Displayposition(3) Height(225) Left(401) Parent(#COM_OWNER) Tabposition(4) Tabstop(False) Top(158) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#EnabledControlPanelLayout)
    Define_Com Class(#PRIM_MD.Switch) Name(#EnableIPMonitoring) Caption('Enabled') Checked(True) Displayposition(2) Iconalignment(CenterRight) Iconcolor(Theme500) Left(44) Parent(#EnabledControlPanel) Tabposition(1) Top(64) Width(790) Height(44) Style(#NavigationLinkStyle) Visible(False)
    Define_Com Class(#PRIM_MD.Label) Name(#EnabledControlPanelHeading) Displayposition(1) Left(44) Parent(#EnabledControlPanel) Tabposition(2) Themedrawstyle('Heading3') Top(24) Width(790) Caption('Monitoring') Height(24) Wordwrap(True)
    
    Define_Com Class(#PRIM_PANL) Name(#CleanupControlPanel) Displayposition(4) Height(335) Left(401) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(407) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#CleanupControlPanelLayout) Visible(False)
    Define_Com Class(#PRIM_MD.Label) Name(#CleanupControlPanelHeading) Displayposition(1) Left(44) Parent(#CleanupControlPanel) Tabposition(1) Themedrawstyle('Heading3') Top(24) Caption('Clearing Bans') Height(24) Wordwrap(True) Width(103)
    Define_Com Class(#PRIM_MD.Label) Name(#BanClearServiceToggle) Displayposition(3) Left(44) Parent(#CleanupControlPanel) Tabposition(2) Top(83) Width(790) Height(44) Iconalignment(CenterRight) Icon('stop') Iconcolor(Theme200) Iconheight(36) Style(#NavigationLinkStyle) Wordwrap(True)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#InvalidAttemptsDropdown) Caption('Request Limit') Displayposition(3) Left(44) Parent(#EnabledControlPanel) Tabposition(3) Top(124) Width(790) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#BanDurationDropdown) Caption('Ban Duration') Displayposition(4) Left(44) Parent(#CleanupControlPanel) Tabposition(4) Top(143) Width(790) Mouseoverstyle(#NavigationLinkStyle)
    
    * Ending check delay
    Define_Com Class(#PRIM_TIMR) Name(#EndingDelay) Startup(Manual) Interval(5000)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#CheckIntervalDropdown) Caption('Check Every') Displayposition(5) Left(44) Parent(#CleanupControlPanel) Tabposition(3) Top(234) Width(790) Mouseoverstyle(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#CurrentValueWarningText) Caption('Current values will be active once process restarts') Displayposition(2) Left(44) Parent(#CleanupControlPanel) Tabposition(5) Themedrawstyle('Subheading+ForegroundDarkAccent') Top(48) Height(19) Width(790) Visible(False) Wordwrap(True)
    
    Define_Com Class(#PRIM_MD.FlatButton) Name(#ViewIPBansAction) Displayposition(5) Left(401) Parent(#COM_OWNER) Tabposition(5) Themedrawstyle('Card+ForegroundMediumPrimary') Top(766) Height(56) Width(880) Icon('keyboard_arrow_right') Captionalignment(CenterLeft) Iconalignment(CenterRight) Iconmarginright(16) Style(#NavigationLinkStyle) Captionmarginleft(24) Visible(False)
    
    * References
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get References
    #SecuritySettings <= (#Data.GetDataClient( #COM_SELF ) *As #BPFSecurityDataClient)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    #COM_SELF.SetStrings
    
    * Setup dropdowns
    #COM_SELF.InitialiseDropDowns
    #ViewIPBansAction.Styles.Add( #RoundedCornerStyle )
    
    * Get Current Status
    #SecuritySettings.GetIPMonitoringStatus
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    Evtroutine Handling(#ViewIPBansAction.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #Navigate.To( 'SECURITYCURRENTIPBANSVIEW' )
    
    Endroutine
    
    * Controls
    Evtroutine Handling(#EnableIPMonitoring.Changed #InvalidAttemptsDropdown.Changed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #SecuritySettings.SetIPMonitoringValues( #EnableIPMonitoring.Checked #InvalidAttemptsDropdown.Value.AsNumber )
    
    Endroutine
    
    Evtroutine Handling(#BanClearServiceToggle.MouseEnter)
    
    #BanClearServiceToggle.IconColor := Theme500
    
    Endroutine
    
    Evtroutine Handling(#BanClearServiceToggle.MouseLeave)
    
    #BanClearServiceToggle.IconColor := Theme200
    
    Endroutine
    
    Evtroutine Handling(#CheckIntervalDropdown.Changed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #SecuritySettings.SetIPMonitoringCheckInterval( #CheckIntervalDropdown.Value.AsNumber )
    
    Endroutine
    
    Evtroutine Handling(#BanDurationDropdown.Changed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #SecuritySettings.SetIPMonitoringCheckBanDuration( #BanDurationDropdown.Value.AsNumber )
    
    Endroutine
    
    Evtroutine Handling(#BanClearServiceToggle.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    Case Of_Field(#BanClearServiceToggle.Icon.UpperCase)
    
    When Value_Is(= STOP)
    
    #SecuritySettings.SetIPBanClearTask( STOP )
    
    When Value_Is(= PLAY_ARROW)
    
    #SecuritySettings.SetIPBanClearTask( START )
    
    Otherwise
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endcase
    
    Endroutine
    
    Evtroutine Handling(#EndingDelay.Tick)
    
    #EndingDelay.Stop
    #SecuritySettings.GetIPMonitoringStatus
    
    Endroutine
    
    * Data
    Evtroutine Handling(#SecuritySettings.OnGetIPMonitoringStatusCompleted)
    
    #COM_SELF.BuildOptions
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnGetIPMonitoringStatusFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<SECURITYSETTINGS.ONGETIPMONITORINGSTATUSFAILED> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSetIPMonitoringValuesCompleted)
    
    #COM_SELF.BuildOptions
    
    If (#SecuritySettings.IPMonitoringClearProcessStatus.UpperCase = RUNNING)
    
    #BPFPortal.ShowToaster( (#STRINGS.Text<MESSAGE.SETTINGSAVED> + ' ' + #STRINGS.Text<SERVICE.MESSAGE.RUN>) )
    
    Else
    
    #BPFPortal.ShowToaster( (#STRINGS.Text<MESSAGE.SETTINGSAVED> + ' ' + #STRINGS.Text<SERVICE.MESSAGE.END>) )
    
    Endif
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSetIPMonitoringValuesFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<MESSAGE.SETTINGSAVED.ERROR> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSetIPMonitoringCheckIntervalCompleted #SecuritySettings.OnSetIPMonitoringCheckBanDurationCompleted)
    
    #CurrentValueWarningText.Visible := #SecuritySettings.IPMonitoringChangesPending
    
    #BPFPortal.ShowToaster( #STRINGS.Text<MESSAGE.SETTINGSAVED> )
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSetIPMonitoringCheckIntervalFailed #SecuritySettings.OnSetIPMonitoringCheckBanDurationFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<MESSAGE.SETTINGSAVED.ERROR> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSetIPMonitoringCheckBanTaskCompleted)
    
    #COM_SELF.BuildOptions
    
    If (#SecuritySettings.IPMonitoringClearProcessStatus.UpperCase = RUNNING)
    
    #BPFPortal.ShowToaster( #STRINGS.Text<SERVICE.MESSAGE.RUN> )
    
    Else
    
    #BPFPortal.ShowToaster( #STRINGS.Text<SERVICE.MESSAGE.END> )
    
    Endif
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSetIPMonitoringCheckBanTaskFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<MESSAGE.SETTINGSAVED.ERROR> 7500 )
    
    Endroutine
    
    *
    * Methods
    *
    
    Mthroutine Name(SetStrings)
    
    #PANELHEADING.CAPTION := #STRINGS.Text<PANELHEADING.CAPTION>
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    #ENABLEDCONTROLPANELHEADING.CAPTION := #STRINGS.Text<ENABLEDCONTROLPANELHEADING.CAPTION>
    #ENABLEIPMONITORING.CAPTION := #STRINGS.Text<ENABLEIPMONITORING.CAPTION>
    #INVALIDATTEMPTSDROPDOWN.CAPTION := #STRINGS.Text<INVALIDATTEMPTSDROPDOWN.CAPTION>
    #CLEANUPCONTROLPANELHEADING.CAPTION := #STRINGS.Text<CLEANUPCONTROLPANELHEADING.CAPTION>
    #CURRENTVALUEWARNINGTEXT.CAPTION := #STRINGS.Text<CURRENTVALUEWARNINGTEXT.CAPTION>
    #BANDURATIONDROPDOWN.CAPTION := #STRINGS.Text<BANDURATIONDROPDOWN.CAPTION>
    #CHECKINTERVALDROPDOWN.CAPTION := #STRINGS.Text<CHECKINTERVALDROPDOWN.CAPTION>
    
    Endroutine
    
    Mthroutine Name(BuildOptions) Access(*PRIVATE)
    
    * Assume we arent ending
    #EnabledControlPanel.Enabled := True
    
    * Set Values
    #EnableIPMonitoring.Checked := #SecuritySettings.IPMonitoringEnabled
    
    * Set Other Contriol Panels if feature enabled
    If (#SecuritySettings.IPMonitoringEnabled)
    
    * Assume we arent ending
    #CleanupControlPanel.Enabled := True
    
    Case Of_Field(#SecuritySettings.IPMonitoringClearProcessStatus.UpperCase)
    
    When Value_Is(= RUNNING)
    
    #BanClearServiceToggle.Icon := 'stop'
    
    When Value_Is(= STOPPED)
    
    #BanClearServiceToggle.Icon := 'play_arrow'
    
    When Value_Is(= ENDING)
    
    #BanClearServiceToggle.Icon := 'hourglass_full'
    
    Endcase
    
    * Set Values
    #InvalidAttemptsDropdown.Value := #SecuritySettings.IPMonitoringAttemptLimit.AsString
    #BanClearServiceToggle.Caption := #STRINGS.Text<('BANCLEARSERVICETOGGLE.CAPTION.' + #SecuritySettings.IPMonitoringClearProcessStatus.UpperCase)>
    #BanDurationDropdown.Value := #SecuritySettings.IPMonitoringBanDuration.AsString
    #CheckIntervalDropdown.Value := #SecuritySettings.IPMonitoringBanCheckInterval.AsString
    #CurrentValueWarningText.Visible := #SecuritySettings.IPMonitoringChangesPending
    
    If (#SecuritySettings.NumberOfIPsBanned = 0)
    
    #ViewIPBansAction.Caption := #STRINGS.Text<VIEWIPBANSACTION.CAPTION.NOBANS>
    
    Else
    
    If (#SecuritySettings.NumberOfIPsBanned < 2)
    
    #ViewIPBansAction.Caption := #STRINGS.Text<VIEWIPBANSACTION.CAPTION.BAN>.Replace( '%%NUM%%' #SecuritySettings.NumberOfIPsBanned.AsString )
    
    Else
    
    #ViewIPBansAction.Caption := #STRINGS.Text<VIEWIPBANSACTION.CAPTION.BANS>.Replace( '%%NUM%%' #SecuritySettings.NumberOfIPsBanned.AsString )
    
    Endif
    
    #ViewIPBansAction.Visible := True
    
    Endif
    
    Endif
    
    If (#SecuritySettings.IPMonitoringClearProcessStatus.UpperCase = ENDING)
    
    #EnabledControlPanel.Enabled := False
    
    If (#CleanupControlPanel.Visible)
    #CleanupControlPanel.Enabled := False
    Endif
    
    #EndingDelay.Start
    
    Endif
    
    #EnableIPMonitoring.Visible #InvalidAttemptsDropdown.Visible #CleanupControlPanel.Visible #ViewIPBansAction.Visible := #SecuritySettings.IPMonitoringEnabled
    
    Endroutine
    
    Mthroutine Name(InitialiseDropDowns) Access(*PRIVATE)
    
    * Set Address Limit
    #InvalidAttemptsDropdown.Items.Add Caption('25') Value('25')
    #InvalidAttemptsDropdown.Items.Add Caption('50') Value('50')
    #InvalidAttemptsDropdown.Items.Add Caption('100') Value('100')
    #InvalidAttemptsDropdown.Items.Add Caption('1000') Value('1000')
    
    * Ban Duration
    #BanDurationDropdown.Items.Add Caption(#STRINGS.Text<BANDURATIONDROPDOWN.ITEM.43200.CAPTION>) Value('43200')
    #BanDurationDropdown.Items.Add Caption(#STRINGS.Text<BANDURATIONDROPDOWN.ITEM.86400.CAPTION>) Value('86400')
    #BanDurationDropdown.Items.Add Caption(#STRINGS.Text<BANDURATIONDROPDOWN.ITEM.172800.CAPTION>) Value('172800')
    #BanDurationDropdown.Items.Add Caption(#STRINGS.Text<BANDURATIONDROPDOWN.ITEM.604800.CAPTION>) Value('604800')
    
    * Check Interval
    #CheckIntervalDropdown.Items.Add Caption(#STRINGS.Text<CHECKINTERVALDROPDOWN.ITEM.900000.CAPTION>) Value('900000')
    #CheckIntervalDropdown.Items.Add Caption(#STRINGS.Text<CHECKINTERVALDROPDOWN.ITEM.1800000.CAPTION>) Value('1800000')
    #CheckIntervalDropdown.Items.Add Caption(#STRINGS.Text<CHECKINTERVALDROPDOWN.ITEM.3600000.CAPTION>) Value('3600000')
    #CheckIntervalDropdown.Items.Add Caption(#STRINGS.Text<CHECKINTERVALDROPDOWN.ITEM.7200000.CAPTION>) Value('7200000')
    
    Endroutine
    
    End_Com
