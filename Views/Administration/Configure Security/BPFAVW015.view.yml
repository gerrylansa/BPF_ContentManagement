Web View:
  Name:               BPFAdminSecurityLoginAttemptsView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal Security Login Attempts View"

  - JPN:
      Description:        "Portal Security Login Attempts View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Componenttag('BPFAPP005')
    
    * Reference Application Objects
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFAdminSecurityLoginAttemptsViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFSecurityDataClient) Name(#SecuritySettings) Reference(*DYNAMIC)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(93) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#MainLayout)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#ControlPanel) Parent(#MainLayout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(24)
    
    
    * Panel Control Layout
    Define_Com Class(#PRIM_TBLO) Name(#ControlLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row1) Displayposition(1) Parent(#ControlLayout) Units(Content) Height(96)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row2) Displayposition(2) Parent(#ControlLayout) Units(Pixels) Height(62)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ControlColumn1) Displayposition(1) Parent(#ControlLayout) Minwidth(44)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ControlColumn2) Displayposition(2) Parent(#ControlLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ControlColumn3) Displayposition(3) Parent(#ControlLayout) Minwidth(44)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#Layout1Item1) Column(#ControlColumn2) Manage(#AttemptsRangeDropDown) Parent(#ControlLayout) Row(#Layout1Row1) Sizing(FitToWidth) Alignment(TopLeft) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ControlLayoutItem1) Alignment(CenterRight) Column(#ControlColumn2) Manage(#PanelAction) Parent(#ControlLayout) Row(#Layout1Row2) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ControlLayoutItem2) Alignment(TopLeft) Column(#ControlColumn1) Manage(#Divider) Parent(#ControlLayout) Row(#Layout1Row2) Sizing(FitToWidth) Columnspan(3)
    
    * Controls
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Caption('Login Attempts') Displayposition(1) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(2) Height(38) Left(350) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('Select the number of login attempts a user is allowed before their account is disabled by the system. The higher the number of attempts the less secure the portal becomes.')
    Define_Com Class(#PRIM_PANL) Name(#ControlPanel) Displayposition(3) Height(160) Left(410) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(141) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#ControlLayout)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#AttemptsRangeDropDown) Caption('Attempts') Displayposition(1) Left(44) Parent(#ControlPanel) Tabposition(1) Top(16) Width(790) Height(80) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#PanelAction) Caption('Save') Displayposition(2) Left(734) Parent(#ControlPanel) Tabposition(2) Themedrawstyle('ForegroundMediumPrimary') Top(109) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_PANL) Name(#Divider) Displayposition(3) Height(8) Left(0) Parent(#ControlPanel) Tabposition(3) Tabstop(False) Themedrawstyle('TopDivider') Top(96) Width(878)
    
    * References
    
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get Reference to logging data client
    #SecuritySettings <= (#Data.GetDataClient( #COM_SELF ) *As #BPFSecurityDataClient)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetStrings
    
    #SecuritySettings.GetAllowedAttempts
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Action
    Evtroutine Handling(#PanelAction.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #SecuritySettings.SaveAllowedAttempts
    
    Endroutine
    
    * Data
    Evtroutine Handling(#AttemptsRangeDropDown.Changed)
    
    #SecuritySettings.AllowedAttempts := #AttemptsRangeDropDown.Value.AsNumber
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnGetAllowedAttemptsCompleted)
    
    #COM_SELF.BuildOptions
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnGetAllowedAttemptsFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<SECURITYSETTINGS.ONGETALLOWEDATTEMPTSFAILED> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSaveAllowedAttemptsCompleted)
    
    #BPFPortal.ShowToaster( #STRINGS.Text<SECURITYSETTINGS.ONSAVEALLOWEDATTEMPTSCOMPLETED> )
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    Evtroutine Handling(#SecuritySettings.OnSaveAllowedAttemptsFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<SECURITYSETTINGS.ONSAVEALLOWEDATTEMPTSFAILED> 7500 )
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #PANELHEADING.CAPTION := #STRINGS.Text<PANELHEADING.CAPTION>
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    #ATTEMPTSRANGEDROPDOWN.CAPTION := #STRINGS.Text<ATTEMPTSRANGEDROPDOWN.CAPTION>
    #PANELACTION.CAPTION := #STRINGS.Text<PANELACTION.CAPTION>
    
    Endroutine
    
    Mthroutine Name(BuildOptions) Access(*PRIVATE)
    
    * Ensure we have a blank Slate
    #AttemptsRangeDropDown.Items.RemoveAll
    
    * Add Options
    #AttemptsRangeDropDown.Items.Add Value('3') Caption('3')
    #AttemptsRangeDropDown.Items.Add Value('5') Caption('5')
    #AttemptsRangeDropDown.Items.Add Value('10') Caption('10')
    #AttemptsRangeDropDown.Items.Add Value('1000') Caption('1000')
    
    * Set Focus Option
    #AttemptsRangeDropDown.Value := #SecuritySettings.AllowedAttempts.AsString
    
    Endroutine
    
    End_Com
