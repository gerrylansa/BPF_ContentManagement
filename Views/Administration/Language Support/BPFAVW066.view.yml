Web View:
  Name:               BPFLanguageResourceStringsView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Language Resource Strings List View"

  - JPN:
      Description:        "Language Resource Strings List View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Verticalscroll(True) Componenttag('BPFAPP011')
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFBrowserEvents) Name(#Browser) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFLanguageResourceStringsViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFLanguageResourcesDataClient) Name(#Resources) Reference(*DYNAMIC)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#LoadMoreDisplayStyle) Fontweight(Weight500) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24) Marginbottom(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainLayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainLayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainLayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(43) Minwidth(140) Maxwidth(440)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainLayoutColumn4) Displayposition(4) Parent(#MainLayout) Width(43) Minwidth(140) Maxwidth(440)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainLayoutColumn5) Displayposition(5) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainLayoutColumn6) Displayposition(6) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow1) Displayposition(1) Parent(#MainLayout) Height(141) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow2) Displayposition(2) Parent(#MainLayout) Units(Pixels) Height(86)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow3) Displayposition(3) Parent(#MainLayout) Height(10) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow4) Displayposition(4) Parent(#MainLayout) Height(80) Units(Content) Minheight(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#MainLayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(ContentWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#MainLayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#MainLayoutColumn3) Manage(#TranslationLabel) Parent(#MainLayout) Row(#MainLayoutRow2) Margintop(24) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem4) Alignment(CenterRight) Column(#MainLayoutColumn4) Parent(#MainLayout) Row(#MainLayoutRow2) Manage(#UserLanguage) Sizing(FitToHeight) Margintop(16) Marginright(18) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem5) Manage(#DummyFooter) Parent(#MainLayout) Row(#MainLayoutRow4) Sizing(FitToWidth) Column(#MainLayoutColumn3) Alignment(TopLeft) Columnspan(2) Flow(Down) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem6) Alignment(TopRight) Column(#MainLayoutColumn5) Manage(#EditResourceAction) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem7) Alignment(TopLeft) Column(#MainLayoutColumn2) Manage(#ExampleCodeLinkText) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Down) Margintop(12) Columnspan(2)
    
    * Main Controls
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Displayposition(2) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(24) Icon('arrow_back') Captionmarginleft(24) Height(48) Wordwrap(True) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Icon) Name(#EditResourceAction) Displayposition(3) Icon('edit') Left(1302) Parent(#COM_OWNER) Tabposition(2) Top(24) Themedrawstyle('ForegroundMediumPrimary') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(4) Height(21) Left(350) Parent(#COM_OWNER) Tabposition(4) Top(96) Width(940) Wordwrap(True) Caption('View the strings for this resource for a given language. Click the edit incon to change a translation')
    Define_Com Class(#PRIM_MD.Label) Name(#DummyFooter) Displayposition(6) Left(410) Parent(#COM_OWNER) Tabposition(5) Themedrawstyle('ForegroundMediumPrimary') Top(285) Height(56) Width(880) Style(#LoadMoreDisplayStyle) Captionalignment(Center)
    Define_Com Class(#PRIM_MD.Label) Name(#TranslationLabel) Caption('Translating') Displayposition(7) Left(410) Parent(#COM_OWNER) Tabposition(6) Top(189) Themedrawstyle('Card+Heading3') Height(62) Width(880) Style(#RoundedCornerStyle) Captionmarginleft(16)
    * Define_Com Class(#PRIM_MD.Dropdown) Name(#UserLanguage) Displayposition(1) Left(1076) Parent(#COM_OWNER) Tabposition(1) Top(181) Height(70) Style(#NavigationLinkStyle) Appearance(None) Themedrawstyle('ForegroundSecondary') Width(196) Icon('language') Iconcolor(Theme500)
    Define_Com Class(#BPFLanguageSelectionControl) Name(#UserLanguage) Left(1076) Parent(#COM_OWNER) Tabposition(7) Tabstop(False) Top(181) Height(70) Width(196)
    
    * References
    Define_Com Class(#BPFLanguageResourceItemClass) Name(#CurrentResource) Reference(*DYNAMIC)
    
    * Control
    Define_Com Class(#PRIM_BOLN) Name(#LanguageSelectLoading)
    Define_Com Class(#PRIM_TIMR) Name(#LoadDelay) Interval(1) Startup(Manual)
    Define_Com Class(#PRIM_NMBR) Name(#LoadIndex)
    
    * Collection
    Define_Com Class(#PRIM_LCOL<#PRIM_TBLO.Item>) Name(#MainLayoutItems)
    Define_Com Class(#PRIM_LCOL<#BPFLanguageResourceStringCard>) Name(#Cards)
    Define_Com Class(#PRIM_MD.Label) Name(#ExampleCodeLinkText) Displayposition(5) Left(350) Parent(#COM_OWNER) Tabposition(3) Top(129) Width(200) Wordwrap(True) Icon('navigate_next') Iconalignment(AfterCaption) Caption('How to use the resource') Themedrawstyle('ForegroundMediumPrimary') Style(#NavigationLinkStyle) Iconmarginleft(8) Captionalignment(Center) Iconmarginright(12) Paddingbottom(6) Paddingtop(6)
    
    * Panels
    
    * Routines
    
    * Events
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get References
    #Resources <= (#Data.GetDataClient( #COM_SELF ) *As #BPFLanguageResourcesDataClient)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get Reference to current user
    #CurrentResource <= #Resources.Resource<#SYS_WEB.URLParameters<VALUE>.value.UpperCase>
    
    * We load data, so set wait for a user navigation
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    * Do we have a resource or do we need to load first
    If (#CurrentResource *IsNot *Null)
    
    * Set Title
    #PanelHeading := #CurrentResource.Description
    
    #UserLanguage.Control := #Browser.UserLanguage
    
    * Set Strings
    #CurrentResource.LoadStrings( #Browser.UserLanguage )
    
    Else
    
    If (#SYS_WEB.URLParameters<VALUE> *IsNot *NULL)
    
    #Resources.GetResourceByIdentifier( #SYS_WEB.URLParameters<VALUE>.value.UpperCase )
    
    Else
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endif
    
    Endif
    
    * Set Strings
    #COM_SELF.SetStrings
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    * Back to Settings
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    Evtroutine Handling(#ExampleCodeLinkText.Click)
    
    #Navigate.To Eventname('languageresourcesusage') Value(#CurrentResource.Identifier.LowerCase)
    
    Endroutine
    
    Evtroutine Handling(#UserLanguage.Changed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    * Empty the screen
    #COM_SELF.RemoveStrings
    #CurrentResource.LoadStrings( #UserLanguage.Control )
    
    Endroutine
    
    Evtroutine Handling(#EditResourceAction.Click)
    
    * Special case, as we need to presevre teh case of iscode.
    #SYS_WEB.History.Add( ('event=languageresourcestringsedit&value=' + #CurrentResource.Identifier + '&lang=' + #UserLanguage.Control) )
    
    Endroutine
    
    * Data
    Evtroutine Handling(#Resources.OnGetResourceCompleted)
    
    * Get Reference to current user
    #CurrentResource <= #Resources.Resource<#SYS_WEB.URLParameters<VALUE>.value.UpperCase>
    
    * Set Title
    #PanelHeading := #CurrentResource.Description
    #UserLanguage.Control := #Browser.UserLanguage
    
    #CurrentResource.LoadStrings( #Browser.UserLanguage )
    
    Endroutine
    
    Evtroutine Handling(#Resources.OnGetResourceFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<RESOURCES.ONGETRESOURCEFAILED> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#CurrentResource.OnLoadStringsCompleted)
    
    #COM_SELF.BuildStrings
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#CurrentResource.OnLoadStringsFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<CURRENTRESOURCE.ONLOADSTRINGSFAILED> 7500 )
    
    Endroutine
    
    * Evtroutine Handling(#Resources.OnGetLanguageOptionsCompleted)
    *
    * #COM_SELF.BuildLanguageSelect
    *
    * Endroutine
    
    Evtroutine Handling(#COM_OWNER.ScrollChanged)
    
    If ((#COM_SELF.VerticalScrollRemainder <= 950) *AndIf (#LoadIndex < #CurrentResource.Strings.ItemCount))
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #LoadDelay.Start
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.SizeChanged)
    
    If (#SYS_WEB.WebPage.Width < 360)
    
    #TranslationLabel.Caption := *null
    
    Else
    
    #TRANSLATIONLABEL.CAPTION := #STRINGS.Text<TRANSLATIONLABEL.CAPTION>
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#LoadDelay.Tick)
    
    #LoadDelay.Stop
    
    #COM_SELF.BuildStrings
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    * Methods
    Mthroutine Name(SetStrings)
    
    #EditResourceAction.Visible := #Resources.AreActive
    
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    
    If (#SYS_WEB.WebPage.Width < 360)
    
    #TranslationLabel.Caption := *null
    
    Else
    
    #TRANSLATIONLABEL.CAPTION := #STRINGS.Text<TRANSLATIONLABEL.CAPTION>
    
    Endif
    
    #ExampleCodeLinkText.Caption := #STRINGS.Text<EXAMPLECODELINKTEXT.CAPTION>
    
    Endroutine
    
    
    Mthroutine Name(BuildStrings) Access(*PRIVATE)
    
    * List Control
    Define_Com Class(#PRIM_NMBR) Name(#UpperIndex)
    Define_Com Class(#PRIM_NMBR) Name(#Index)
    
    * For each string in the resource
    For Each(#String) In(#CurrentResource.Strings)
    
    #Index += 1
    Continue If(#Index <= #LoadIndex)
    
    #COM_SELF.AddStringCard( #String )
    
    #UpperIndex += 1
    Leave If(#UpperIndex = 25)
    
    Endfor
    
    #LoadIndex := #Index
    
    Endroutine
    
    Mthroutine Name(RemoveStrings)
    
    #LoadIndex := 0
    
    #MainLayoutItems.RemoveAll
    #Cards.RemoveAll
    
    Endroutine
    
    
    Mthroutine Name(AddStringCard)
    Define_Map For(*INPUT) Class(#BPFResourceStringItemClass) Name(#CurrentString) Pass(*BY_REFERENCE)
    
    * This is what we are making
    * Define_Com Class(#BPFSessionEventsCard) Name(#BPFSessionEventsCard) Displayposition(5) Left(410) Parent(#COM_OWNER) Tabposition(5) Tabstop(False) Top(300) Width(880)
    
    * The order of the code below seems to stop cards to appear as if the jump down onto the page. The cost is performance, and a 25 items per limit load.
    * Will investigate using a Tree view and tree view item design in the future.
    
    * First we need a layout item and parent
    #MainLayoutItems.insert( #Com_self.CreateLayoutItem )
    #MainLayoutItems.Last.Parent <= #MainLayout
    
    * Now for the card
    #Cards.Insert( (*New #BPFLanguageResourceStringCard) )
    
    * Put card under management and parent
    #MainLayoutItems.Last.Manage <= #Cards.Last
    #Cards.last.Parent <= #COM_OWNER
    
    * Set card properties
    #Cards.Last.DisplayPosition := (#MainLayoutItems.ItemCount + 6)
    #Cards.Last.SetRelatedReference( #CurrentString )
    
    Endroutine
    
    Mthroutine Name(CreateLayoutItem)
    Define_Map For(*RESULT) Class(#PRIM_TBLO.Item) Name(#Result) Pass(*BY_REFERENCE)
    
    #Result <= (*New #PRIM_TBLO.Item)
    
    * We are trying to build either
    * Card -   Define_Com Class(#PRIM_TBLO.Item) Alignment(TopLeft) Column(#MainLayoutColumn3) Row(#MainLayoutRow3) Sizing(ContentHeightFitToWidth) Margintop(16) Columnspan(2) Flow(Down)
    
    * Common Properties
    
    #Result.Alignment := TopLeft
    #Result.Column <= #MainLayoutColumn3
    #Result.ColumnSpan := 2
    #Result.Row <= #MainLayoutRow3
    #Result.Flow := Down
    
    #Result.Sizing := ContentHeightFitToWidth
    #Result.MarginTop := 16
    
    Endroutine
    
    
    * Mthroutine Name(BuildLanguageSelect) Access(*PRIVATE)
    *
    * * Prevent unwanted actions
    * * #LanguageSelectLoading := True
    *
    * If (#Resources.Languages.IsEmpty)
    *
    * #Resources.GetLanguageOptions
    *
    * Else
    *
    * #CurrentResource.LoadStrings( #Browser.UserLanguage )
    *
    * For Each(#Option) In(#Resources.Languages)
    *
    * #UserLanguage.Items.Add Value(#Option.IsoCode) Caption(#Option.Description)
    *
    * Endfor
    *
    * #UserLanguage := #Browser.UserLanguage
    *
    * Endif
    *
    * * Allow all actions again
    * * #LanguageSelectLoading := False
    *
    * Endroutine
    
    End_Com
