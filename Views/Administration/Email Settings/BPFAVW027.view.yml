Web View:
  Name:               BPFEmailTemplatePreview
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal Email Templates Preview"

  - JPN:
      Description:        "Portal Email Templates Preview"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Verticalscroll(True) Componenttag('BPFAPP004')
    
    * Reference Application Objects
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFEmailTemplatePreviewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFEmailSettingsDataClient) Name(#EmailSettings) Reference(*DYNAMIC)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_VS.Style) Name(#EmailBodyStyle) Borderbottom(8) Borderleft(8) Borderright(8) Bordertop(8) Borderbrush(#Brush1) Foregroundbrush(#Brush2)
    Define_Com Class(#PRIM_VS.SolidBrush) Name(#Brush2) Color(0:0:0)
    Define_Com Class(#PRIM_VS.SolidBrush) Name(#Brush1) Color(245:245:245)
    
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24) Marginbottom(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(93) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow1) Displayposition(2) Parent(#MainLayout) Height(487) Units(Content)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#TemplatesPanel) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24)
    
    * Everything Panel Layout
    Define_Com Class(#PRIM_TBLO) Name(#TemplatesPanelLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#TemplatesPanelLayoutRow1) Displayposition(1) Parent(#TemplatesPanelLayout) Units(Content) Height(437)
    Define_Com Class(#PRIM_TBLO.Row) Name(#TemplatesPanelLayoutRow2) Displayposition(2) Parent(#TemplatesPanelLayout) Units(Pixels) Height(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#TemplatesPanelLayoutColumn1) Displayposition(1) Parent(#TemplatesPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#TemplatesPanelLayoutColumn2) Displayposition(2) Parent(#TemplatesPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#TemplatesPanelLayoutColumn3) Displayposition(3) Parent(#TemplatesPanelLayout) Minwidth(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem1) Manage(#TemplatesHeading) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(FitToWidth) Column(#TemplatesPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Marginright(36) Margintop(12)
    
    * Main Controls
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem2) Manage(#TemplateSubjectLine) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#TemplatesPanelLayoutColumn2) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem3) Manage(#TemplateFromAddressLine) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#TemplatesPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem4) Manage(#TemplateFromAddressLine) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#TemplatesPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem5) Alignment(TopLeft) Column(#TemplatesPanelLayoutColumn2) Manage(#TemplateBody) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem6) Alignment(TopLeft) Column(#TemplatesPanelLayoutColumn2) Manage(#TemplateToAddressLine) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Marginbottom(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TemplatesPanelLayoutItem7) Alignment(TopRight) Column(#TemplatesPanelLayoutColumn2) Manage(#TemplateDownload) Parent(#TemplatesPanelLayout) Row(#TemplatesPanelLayoutRow1) Sizing(None) Marginleft(12) Margintop(12)
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Displayposition(1) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Caption('Preview Email') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Displayposition(2) Height(38) Left(350) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('This is a preview of how email template might look to the end user as email clients can vary in how they display HTML emails. You can customise this email by clicking on the edit icon.')
    
    * Panels
    
    * Notifications
    Define_Com Class(#PRIM_PANL) Name(#TemplatesPanel) Displayposition(3) Height(463) Left(410) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(141) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#TemplatesPanelLayout)
    
    * Panel Controls
    Define_Com Class(#PRIM_MD.Label) Name(#TemplatesHeading) Displayposition(1) Left(24) Parent(#TemplatesPanel) Tabposition(2) Themedrawstyle('Heading3') Top(12) Width(794) Wordwrap(True) Height(48) Icon('edit') Iconalignment(CenterRight) Iconcolor(Theme500) Style(#NavigationLinkStyle) Captionmarginbottom(0) Captionmarginleft(0) Captionmarginright(0) Captionmargintop(0) Iconmarginright(12)
    Define_Com Class(#PRIM_MD.Label) Name(#TemplateSubjectLine) Displayposition(2) Left(24) Parent(#TemplatesPanel) Tabposition(5) Themedrawstyle('Heading3') Top(60) Width(830) Wordwrap(True) Height(10)
    Define_Com Class(#PRIM_MD.Label) Name(#TemplateFromAddressLine) Displayposition(3) Left(24) Parent(#TemplatesPanel) Tabposition(4) Top(94) Width(830) Wordwrap(True) Height(10)
    Define_Com Class(#PRIM_MD.Label) Name(#TemplateToAddressLine) Displayposition(4) Left(24) Parent(#TemplatesPanel) Tabposition(3) Top(104) Width(830) Wordwrap(True) Height(19) Caption('to me') Themedrawstyle('Subheading')
    Define_Com Class(#PRIM_WEB.HtmlContainer) Name(#TemplateBody) Description('Email body') Displayposition(5) Height(298) Parent(#TemplatesPanel) Tabposition(1) Tabstop(False) Width(830) Left(24) Top(139) Style(#EmailBodyStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#TemplateDownload) Displayposition(6) Icon('cloud_download') Left(818) Parent(#TemplatesPanel) Tabposition(6) Top(12) Captionmarginbottom(0) Captionmarginleft(0) Captionmarginright(0) Captionmargintop(0) Height(48) Width(48) Iconcolor(Theme500) Iconalignment(Center) Style(#NavigationLinkStyle)
    
    * References
    Define_Com Class(#BPFEmailTemplateDataClass) Name(#CurrentTemplate) Reference(*DYNAMIC)
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get Reference to logging data client
    #EmailSettings <= (#Data.GetDataClient( #COM_SELF ) *As #BPFEmailSettingsDataClient)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * We load data, so set wait for a user navigation
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    
    * Get Reference to logging data client
    #CurrentTemplate <= #EmailSettings.Template<#SYS_WEB.URLParameters<VALUE>.value>
    
    If (#CurrentTemplate *IsNot *Null)
    
    #PanelHeading := #STRINGS.Text<PANELHEADING.CAPTION> + ' ' + #CurrentTemplate.DisplayName
    
    * Get portal logging settings
    #CurrentTemplate.Load( #SYS_WEB.URLParameters<LANG>.value )
    
    Else
    
    #EmailSettings.LoadEmailTemplate( #SYS_WEB.URLParameters<LANG>.value #SYS_WEB.URLParameters<VALUE>.value )
    
    Endif
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    Evtroutine Handling(#TemplatesHeading.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    * #Navigate.to Eventname(EMAILTEMPLATEEDITVIEW) Value(#CurrentTemplate.TemplateID)
    
    * Special case navigation
    #sys_web.history.add( ('event=emailtemplateeditview&value=' + #CurrentTemplate.TemplateID + '&lang=' + #SYS_WEB.URLParameters<LANG>.value) )
    
    Endroutine
    
    Evtroutine Handling(#TemplateDownload.Click)
    
    #CurrentTemplate.Download
    
    Endroutine
    
    * Data
    Evtroutine Handling(#EmailSettings.OnLoadEmailTemplateCompleted)
    
    * Get Reference to logging data client
    #CurrentTemplate <= #EmailSettings.Template<#SYS_WEB.URLParameters<VALUE>.value>
    
    If (#CurrentTemplate *IsNot *null)
    
    #PanelHeading := #STRINGS.Text<PANELHEADING.CAPTION> + ' ' + #CurrentTemplate.DisplayName
    
    * Get portal logging settings
    #CurrentTemplate.Load( #SYS_WEB.URLParameters<LANG>.value )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#EmailSettings.OnLoadEmailTemplateFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<EMAILSETTINGS.ONLOADEMAILTEMPLATEFAILED> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#CurrentTemplate.OnLoadCompleted)
    
    #COM_SELF.BuildPanel
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#CurrentTemplate.OnLoadFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<CURRENTTEMPLATE.ONLOADFAILED> 7500 )
    
    Endroutine
    
    * Methods
    Mthroutine Name(BuildPanel) Access(*PRIVATE)
    
    * Set Subject and Address lines
    #TemplateSubjectLine := #CurrentTemplate.PropertyValue<SUBJECT>
    #TemplateFromAddressLine := #CurrentTemplate.PropertyValue<FROM_NAME> + ' <' + #CurrentTemplate.PropertyValue<FROM_ADDRESS> + '>'
    
    * Set Body
    #TemplateBody.Html := #CurrentTemplate.Body
    
    Endroutine
    
    End_Com
