Web View:
  Name:               BPFContentShareCreateView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal Create Content Share View"

  - JPN:
      Description:        "Portal Create Content Share View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Verticalscroll(True) Componenttag('BPFAPP012')
    
    * Reference Application Objects
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFContentShareCreateViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFContentSharingDataClient) Name(#Sharing) Reference(*DYNAMIC)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24) Marginbottom(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(76) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow1) Displayposition(2) Parent(#MainLayout) Height(221) Units(Content)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#SettingsPanel) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(24)
    
    * Settings Panel Layout
    Define_Com Class(#PRIM_TBLO) Name(#SettingsPanelLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#SettingsPanelLayoutRow1) Displayposition(1) Parent(#SettingsPanelLayout) Units(Content) Height(139)
    Define_Com Class(#PRIM_TBLO.Row) Name(#SettingsPanelLayoutRow4) Displayposition(2) Parent(#SettingsPanelLayout) Units(Pixels) Height(56)
    Define_Com Class(#PRIM_TBLO.Column) Name(#SettingsPanelLayoutColumn1) Displayposition(1) Parent(#SettingsPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#SettingsPanelLayoutColumn2) Displayposition(2) Parent(#SettingsPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#SettingsPanelLayoutColumn4) Displayposition(3) Parent(#SettingsPanelLayout) Minwidth(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#SettingsPanelLayoutItem1) Manage(#SettingsHeading) Parent(#SettingsPanelLayout) Row(#SettingsPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#SettingsPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#SettingsPanelLayoutItem2) Alignment(CenterRight) Column(#SettingsPanelLayoutColumn2) Manage(#CreateAction) Parent(#SettingsPanelLayout) Row(#SettingsPanelLayoutRow4) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#SettingsPanelLayoutItem3) Alignment(TopLeft) Column(#SettingsPanelLayoutColumn2) Manage(#ContentShareNameEdit) Parent(#SettingsPanelLayout) Row(#SettingsPanelLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(16)
    
    * Main Controls
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Displayposition(1) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Caption('Create Share') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(2) Height(21) Left(350) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('Enter a name for the content share you wish to create. Once created you will be taken to your new share so you can start adding items')
    
    * Panels
    
    * Settings
    Define_Com Class(#PRIM_PANL) Name(#SettingsPanel) Displayposition(3) Height(197) Left(410) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(124) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#SettingsPanelLayout)
    
    * Panel Controls
    Define_Com Class(#PRIM_MD.Label) Name(#SettingsHeading) Caption('New Share') Displayposition(1) Left(24) Parent(#SettingsPanel) Tabposition(1) Themedrawstyle('Heading3') Top(24) Width(830) Wordwrap(True) Height(24) Iconalignment(CenterRight)
    Define_Com Class(#PRIM_MD.Edit) Name(#ContentShareNameEdit) Caption('Name') Displayposition(3) Left(24) Parent(#SettingsPanel) Tabposition(3) Top(64) Width(830)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#CreateAction) Caption('CREATE') Displayposition(2) Left(754) Parent(#SettingsPanel) Tabposition(2) Themedrawstyle('ForegroundMediumPrimary') Top(149) Style(#NavigationLinkStyle)
    
    * Routines
    
    * Events
    
    * Self
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get References
    #Sharing <= (#Data.GetDataClient( #COM_SELF ) *As #BPFContentSharingDataClient)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    #COM_SELF.SetStrings
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Fields
    Evtroutine Handling(#ContentShareNameEdit.Changed)
    
    #ContentShareNameEdit.ClearError
    
    Endroutine
    
    
    * Data
    Evtroutine Handling(#Sharing.OnCreateShareCompleted) Id(#NewID)
    
    #Navigate.To Eventname(EDITCONTENTSHAREVIEW) Value(#NewID) Type(REPLACE) Signalreplaceevent(True)
    
    Endroutine
    
    Evtroutine Handling(#Sharing.OnCreateShareFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<'SHARING.ONCREATESHAREFAILED'> 7500 )
    
    Endroutine
    
    * Actions
    
    Evtroutine Handling(#CreateAction.Click #ContentShareNameEdit.Enter)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    If (#COM_SELF.IsValidRequest)
    
    #Sharing.CreateShare( #ContentShareNameEdit.Value )
    
    Else
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endif
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #PANELHEADING.CAPTION := #STRINGS.Text<PANELHEADING.CAPTION>
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    #SETTINGSHEADING.CAPTION := #STRINGS.Text<SETTINGSHEADING.CAPTION>
    #CONTENTSHARENAMEEDIT.CAPTION := #STRINGS.Text<'CONTENTSHARENAMEEDIT.CAPTION'>
    #CREATEACTION.CAPTION := #STRINGS.Text<CREATEACTION.CAPTION>
    
    Endroutine
    
    Mthroutine Name(IsValidRequest)
    Define_Map For(*RESULT) Class(#PRIM_BOLN) Name(#Result)
    
    * Lets clear errors
    #Result := True
    #ContentShareNameEdit.ClearError
    
    * Check Content Share has a name
    If (#ContentShareNameEdit.Value.IsNull)
    
    #ContentShareNameEdit.ShowError( #STRINGS.Text<'CONTENTSHARENAMEEDIT.ERROR.MISSING'> )
    
    Endif
    
    * Did we make it
    * Check if we have any errors
    For Each(#control) In(#SettingsPanel.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.Edit)
    
    If (#control.HasError)
    
    #Result := False
    Return
    
    Endif
    
    Endfor
    
    Endroutine
    
    End_Com
