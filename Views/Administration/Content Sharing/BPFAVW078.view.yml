Web View:
  Name:               BPFContentLibraryListView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Content Library List View"

  - JPN:
      Description:        "Content Library List View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Height(880) Width(1700) Verticalscroll(True) Layoutmanager(#MainDesktopLayout) Componenttag('BPFAPP012')
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFContentLibraryListViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFContentSharingDataClient) Name(#Sharing) Reference(*DYNAMIC)
    
    * Panel Styles
    Define_Com Class(#PRIM_VS.Style) Name(#ListRoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8)
    Define_Com Class(#PRIM_VS.Style) Name(#LoadMoreDisplayStyle) Fontweight(Weight500)
    
    * Main Panel Layouts
    
    * Desktop
    Define_Com Class(#PRIM_TBLO) Name(#MainDesktopLayout) Margintop(24) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn1) Displayposition(1) Parent(#MainDesktopLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn2) Displayposition(2) Parent(#MainDesktopLayout) Width(98) Minwidth(280) Maxwidth(840)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn5) Displayposition(3) Parent(#MainDesktopLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow1) Displayposition(1) Parent(#MainDesktopLayout) Height(243) Units(Content) Minheight(220)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow2) Displayposition(2) Parent(#MainDesktopLayout) Height(460) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow4) Displayposition(3) Parent(#MainDesktopLayout) Units(Pixels) Height(120)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem1) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#Avatar) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem2) Column(#MainDesktopLayoutColumn2) Manage(#PanelTitle) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Alignment(TopCenter) Margintop(17) Marginleft(24) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem3) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#PanelExplanation) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentHeightFitToWidth) Marginleft(24) Marginright(24) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem4) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#Search) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(24) Marginright(2) Marginleft(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem8) Column(#MainDesktopLayoutColumn2) Manage(#FooterText) Parent(#MainDesktopLayout) Sizing(ContentHeightFitToWidth) Alignment(TopCenter) Row(#MainDesktopLayoutRow4) Marginleft(24) Marginright(24) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem5) Alignment(TopLeft) Column(#MainDesktopLayoutColumn2) Manage(#PanelNavigationIcon) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem6) Manage(#ContentPanel) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow2) Sizing(ContentHeightFitToWidth) Column(#MainDesktopLayoutColumn2) Alignment(TopLeft) Margintop(24) Flow(Down)
    
    * Panel Layout
    
    Define_Com Class(#PRIM_TBLO) Name(#ContentPanelLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ContentPanelLayoutColumn1) Displayposition(1) Parent(#ContentPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ContentPanelLayoutColumn2) Displayposition(2) Parent(#ContentPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ContentPanelLayoutColumn3) Displayposition(3) Parent(#ContentPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Row) Name(#ContentPanelLayoutRow2) Displayposition(2) Parent(#ContentPanelLayout) Height(292) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#ContentPanelLayoutRow3) Displayposition(3) Parent(#ContentPanelLayout) Height(80) Units(Content) Minheight(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Row) Name(#ContentPanelLayoutRow1) Displayposition(1) Parent(#ContentPanelLayout) Height(62) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentPanelLayoutItem4) Alignment(TopLeft) Column(#ContentPanelLayoutColumn1) Manage(#ContentItemList) Parent(#ContentPanelLayout) Row(#ContentPanelLayoutRow2) Columnspan(3) Sizing(ContentHeightFitToWidth) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentPanelLayoutItem5) Manage(#ContentItemListActionLabel) Parent(#ContentPanelLayout) Row(#ContentPanelLayoutRow3) Column(#ContentPanelLayoutColumn1) Alignment(TopLeft) Columnspan(3) Sizing(FitToWidth) Margintop(24)
    
    * List Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentPanelLayoutItem2) Alignment(BottomLeft) Column(#ContentPanelLayoutColumn1) Manage(#Divider) Parent(#ContentPanelLayout) Row(#ContentPanelLayoutRow1) Sizing(FitToWidth) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentPanelLayoutItem1) Alignment(CenterLeft) Column(#ContentPanelLayoutColumn1) Manage(#AddNewContentButton) Parent(#ContentPanelLayout) Row(#ContentPanelLayoutRow1) Columnspan(3) Sizing(ContentWidthAndHeight) Marginleft(6)
    
    Define_Com Class(#PRIM_TBLO) Name(#ContentItemListLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ContentItemListLayoutColumn1) Displayposition(1) Parent(#ContentItemListLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#ContentItemListLayoutRow1) Displayposition(1) Parent(#ContentItemListLayout)
    
    * List Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentItemListLayoutItem1) Alignment(CenterLeft) Column(#ContentItemListLayoutColumn1) Manage(#ContentItemIcon) Parent(#ContentItemListLayout) Row(#ContentItemListLayoutRow1) Sizing(ContentWidthAndHeight) Marginleft(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentItemListLayoutItem2) Alignment(CenterLeft) Column(#ContentItemListLayoutColumn1) Flow(CenterVertical) Manage(#ContentItemPrimaryText) Marginleft(80) Parent(#ContentItemListLayout) Row(#ContentItemListLayoutRow1) Sizing(ContentHeightFitToWidth) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentItemListLayoutItem3) Alignment(CenterLeft) Column(#ContentItemListLayoutColumn1) Flow(CenterVertical) Manage(#ContentItemSecondaryText) Marginleft(80) Parent(#ContentItemListLayout) Row(#ContentItemListLayoutRow1) Sizing(ContentHeightFitToWidth) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ContentItemListLayoutItem5) Alignment(CenterRight) Column(#ContentItemListLayoutColumn1) Manage(#ContentItemSizeText) Parent(#ContentItemListLayout) Row(#ContentItemListLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Left) Marginright(24) Marginbottom(10)
    
    * Panel Controls
    Define_Com Class(#PRIM_MD.Icon) Name(#PanelNavigationIcon) Displayposition(1) Icon('arrow_back') Left(430) Parent(#COM_OWNER) Tabposition(6) Top(24) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Icon) Name(#Avatar) Displayposition(2) Height(100) Icon('content_copy') Left(800) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('MediumTitle+Rounded') Top(24) Width(100) Iconheight(70) Iconpadding(10) Paddingbottom(5) Paddingleft(5) Paddingright(5) Paddingtop(5)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelTitle) Caption('Manage Content Library') Displayposition(3) Left(454) Parent(#COM_OWNER) Tabposition(3) Themedrawstyle('Title') Top(141) Width(792) Captionalignment(Center) Height(47) Wordwrap(True)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelExplanation) Displayposition(4) Left(454) Parent(#COM_OWNER) Tabposition(4) Themedrawstyle('ForegroundTheme+Heading3') Top(204) Height(24) Width(792) Wordwrap(True) Captionalignment(Center) Caption('You can add new content to the library or edit content by selecting the content below')
    
    * Search Field
    Define_Com Class(#PRIM_MD.Edit) Name(#Search) Appearance(EditBox) Captionposition(None) Displayposition(5) Height(48) Helperposition(None) Icon('search') Left(432) Parent(#COM_OWNER) Placeholder('Search for resource by name,description or indentifier...') Tabposition(1) Top(252) Width(836) Style(#RoundedCornerStyle) Themedrawstyle('Text(48,48,48,1)')
    
    * Cards
    Define_Com Class(#PRIM_PANL) Name(#ContentPanel) Displayposition(7) Height(436) Left(430) Parent(#COM_OWNER) Tabposition(7) Tabstop(False) Top(324) Width(840) Themedrawstyle('Card') Style(#ListRoundedCornerStyle) Layoutmanager(#ContentPanelLayout)
    
    
    * Account List
    Define_Com Class(#PRIM_MD.List) Name(#ContentItemList) Displayposition(1) Layoutmanager(#ContentItemListLayout) Left(0) Parent(#ContentPanel) Rowheight(72) Tabposition(1) Top(62) Height(292) Width(838)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContentItemIcon) Displayposition(1) Icon('insert_drive_file') Left(24) Parent(#ContentItemList) Tabposition(1) Width(24) Iconalignment(AboveCaption) Captionalignment(Center) Height(24) Top(24)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContentItemPrimaryText) Caption('Resource Description') Displayposition(4) Height(24) Left(80) Parent(#ContentItemList) Tabposition(3) Top(14) Width(734) Wordwrap(True) Themedrawstyle('Heading3')
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContentItemSecondaryText) Displayposition(5) Height(21) Left(80) Parent(#ContentItemList) Tabposition(4) Themedrawstyle('ForegroundSecondary') Top(38) Width(734) Caption('ResourceObjectName')
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContentItemSizeText) Caption('000 KB') Displayposition(2) Dragstyle(None) Left(765) Parent(#ContentItemList) Tabposition(5) Top(16) Height(21) Width(49) Iconalignment(AfterCaption)
    
    * List Action Label
    Define_Com Class(#PRIM_MD.Label) Name(#ContentItemListActionLabel) Caption('Load more') Displayposition(2) Left(0) Parent(#ContentPanel) Tabposition(2) Themedrawstyle('ForegroundMediumPrimary') Top(378) Height(56) Width(838) Style(#LoadMoreDisplayStyle) Captionalignment(Center)
    
    * Footer
    Define_Com Class(#PRIM_MD.Label) Name(#FooterText) Captionalignment(TopCenter) Displayposition(6) Height(45) Parent(#COM_OWNER) Tabposition(5) Top(784) Width(792) Wordwrap(True) Paddingbottom(24) Left(454) Caption('Footer Text')
    Define_Com Class(#PRIM_PANL) Name(#Divider) Displayposition(3) Height(1) Left(0) Parent(#ContentPanel) Tabposition(3) Tabstop(False) Themedrawstyle('TopDivider') Top(61) Width(838)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#AddNewContentButton) Caption('New Content') Displayposition(4) Left(6) Parent(#ContentPanel) Tabposition(4) Themedrawstyle('ForegroundMediumPrimary') Top(9) Height(44) Width(179) Icon('add') Captionalignment(CenterLeft) Captionmarginleft(14) Paddingright(20) Captionmarginright(16) Iconmarginleft(16) Iconheight(32) Paddingbottom(6) Paddingtop(6) Style(#RoundedStyle)
    
    * References
    Define_Com Class(#BPFContentLibraryAddItemSelection) Name(#Menu) Reference(*DYNAMIC)
    
    * Global Variables
    Define_Com Class(#prim_boln) Name(#MoreResults)
    Define_Com Class(#PRIM_TIMR) Name(#DrawDelay) Interval(25) Startup(Manual)
    Define_Com Class(#PRIM_TIMR) Name(#ListEntryDelay) Interval(1) Startup(Manual)
    Define_Com Class(#PRIM_TIMR) Name(#ScrollDelay) Interval(5) Startup(Manual)
    
    * Define_Com Class(#PRIM_TREE.TreeItem) Name(#MouseOverItem) Reference(*DYNAMIC)
    * Define_Com Class(#prim_nmbr) Name(#CurrentScrollPos)
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get References
    #Sharing <= (#Data.GetDataClient( #COM_SELF ) *As #BPFContentSharingDataClient)
    
    * Persist the view
    #Navigate.CurrentEvent.PersistView := True
    
    * Get search value
    If (#SYS_WEB.URLParameters<VALUE> *IsNot *Null)
    #Search := #SYS_WEB.URLParameters<VALUE>.Value
    Endif
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Set Menu Display
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetListActionForLoading
    
    #COM_SELF.SetStrings
    
    * if we dont have the API get it as we could need it
    If (#Sharing.YouTubeDataAPIKey.IsNull)
    
    #Sharing.GetKey
    
    Endif
    
    #Sharing.GetLibraryItems( #Search )
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Prepare)
    
    #Data.KeepDataClient( #COM_SELF )
    
    Endroutine
    
    * Panel Navigation
    Evtroutine Handling(#PanelNavigationIcon.Click)
    
    * Back to Settings
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Search
    Evtroutine Handling(#Search.GotFocus)
    
    #Search.PromptIcon := 'close'
    
    Endroutine
    
    Evtroutine Handling(#Search.Prompting)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetListActionForLoading
    
    #Search.value := *null
    
    * Update the URL to store the current search values
    #SYS_WEB.History.Replace( ('event=' + #Navigate.CurrentEvent.EventName.lowercase + '&value=' + #Search) False )
    
    #Sharing.GetLibraryItems( #Search )
    
    Endroutine
    
    Evtroutine Handling(#Search.LostFocus)
    
    #Search.PromptIcon := *null
    
    Endroutine
    
    Evtroutine Handling(#Search.Enter #Search.IconClick)
    
    If (#COM_SELF.Visible)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetListActionForLoading
    
    * Update the URL to store the current search values
    #SYS_WEB.History.Replace( ('event=' + #Navigate.CurrentEvent.EventName.lowercase + '&value=' + #Search) False )
    
    #Sharing.GetLibraryItems( #Search )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    If ((#COM_SELF.Visible) *AndIf (#ContentItemList.Items.IsEmpty.IsFalse))
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetListActionForLoading
    
    #Sharing.GetLibraryItems( #Search )
    
    Endif
    
    Endroutine
    
    * Data
    Evtroutine Handling(#Sharing.OnGetLibraryItemsCompleted) Moreresult(#More)
    
    #MoreResults := #More
    #COM_SELF.HandleBuildList
    
    Endroutine
    
    Evtroutine Handling(#Sharing.OnGetLibraryItemsFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<'SHARING.ONGETLIBRARYITEMSFAILED'> 7500 )
    
    Endroutine
    
    Evtroutine Handling(#Sharing.ContentLibraryItemUpdated) Item(#Item)
    
    #COM_SELF.HandleUpdateList( #Item )
    
    Endroutine
    
    Evtroutine Handling(#Sharing.ContentLibraryItemDeleted) Item(#Item)
    
    #COM_SELF.HandleRemoveItemInList( #Item )
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    * Timers
    Evtroutine Handling(#DrawDelay.Tick)
    
    #DrawDelay.Stop
    
    #COM_SELF.SetListActionAfterLoad
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    * Okay, the next 4 events are here to deal with list jumping in IE and Chromiumn based browsers
    * It should noted that Firefox actually works as intended, thats a first. So the issue is the list jumps
    * due to focus suddenly being paid attention to. This causes scrolling which we can see. So if a sudden
    * scroll happen this code causes it to be treated as a click. After reading this, have a "drink".
    * Evtroutine Handling(#COM_OWNER.ScrollChanged) Com_Sender(#sender)
    *
    * If ((#Session.IsFireFox))
    * Return
    * Endif
    *
    * If ((#MouseOverItem *IsNot *Null) *And ((#CurrentScrollPos < 400) *Or (#Session.IsIE)) *And ((#COM_SELF.VerticalScrollPos > 407) *And (#COM_SELF.VerticalScrollPos < 410)))
    *
    * #MouseOverItem <= *null
    * #COM_SELF.VerticalScrollPos := #CurrentScrollPos
    *
    *
    * #Navigate.To Eventname(LANGUAGERESOURCESTRINGSVIEW) Value((#ResourceList.Items<#ListEntry>.RelatedReference *As #BPFLanguageResourceItemClass).Identifier)
    *
    *
    * Endif
    *
    * Endroutine
    *
    * Evtroutine Handling(#ResourceList.ItemMouseEnter) Item(#Item)
    *
    * #MouseOverItem <= #Item
    * #ListEntryDelay.Start
    *
    * #CurrentScrollPos := #COM_SELF.VerticalScrollPos
    *
    * Endroutine
    *
    * Evtroutine Handling(#ListEntryDelay.Tick)
    *
    * #ListEntryDelay.Stop
    *
    * If (#MouseOverItem *IsNot *Null)
    * #ListEntry := #MouseOverItem.Entry
    * Endif
    *
    * Endroutine
    *
    * Evtroutine Handling(#ResourceList.MouseLeave)
    *
    * #MouseOverItem <= *null
    * #ListEntry := 0
    *
    * Endroutine
    
    Evtroutine Handling(#AddNewContentButton.Click)
    
    #COM_SELF.HandleAddSelection
    
    Endroutine
    
    Evtroutine Handling(#ContentItemList.ItemClick)
    
    #Navigate.To Eventname(CONTENTITEMDETAILSVIEW) Value((#ContentItemList.CurrentItem.RelatedReference *As #BPFContentLibraryItemClass).Identifier)
    
    Endroutine
    
    Evtroutine Handling(#ContentItemListActionLabel.Click)
    
    * Action only a load more situation
    If (#ContentItemListActionLabel.ComponentTag <> MORE)
    Return
    Endif
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetListActionForLoading( False )
    
    #COM_SELF.HandleBuildList
    
    Endroutine
    
    * Methods
    Mthroutine Name(SetStrings)
    
    #PANELTITLE.CAPTION := #STRINGS.Text<PANELTITLE.CAPTION>
    #PANELEXPLANATION.CAPTION := #STRINGS.Text<PANELEXPLANATION.CAPTION>
    #SEARCH.PLACEHOLDER := #STRINGS.Text<SEARCH.PLACEHOLDER>
    #ADDNEWCONTENTBUTTON.CAPTION := #STRINGS.Text<'ADDNEWCONTENTBUTTON.CAPTION'>
    #FOOTERTEXT.CAPTION := #STRINGS.Text<FOOTERTEXT.CAPTION>
    
    Endroutine
    
    Mthroutine Name(HandleBuildList) Access(*PRIVATE)
    
    * List Control
    Define_Com Class(#PRIM_NMBR) Name(#UpperIndex)
    Define_Com Class(#PRIM_NMBR) Name(#Index)
    
    * Back to for loop
    For Each(#Item) In(#Sharing.ContentLibrary)
    
    #index := #index + 1
    Continue If(#Index <= #ContentItemList.Items.ItemCount)
    
    #COM_SELF.AddContentItemToList( #Item )
    
    #UpperIndex := #UpperIndex + 1
    Leave If(#UpperIndex = 25)
    
    Endfor
    
    * Set Message
    #DrawDelay.Start
    
    Endroutine
    
    Mthroutine Name(HandleUpdateList)
    Define_Map For(*INPUT) Class(#BPFContentLibraryItemClass) Name(#UpdateItem) Pass(*BY_REFERENCE)
    
    * Check to see if we have the item in the list, as you can only edit anm item that can be selected
    If (#ContentItemList.FindReference( #UpdateItem ) *IsNot *Null)
    
    * Just update in place, so the user gets visual feedback
    #COM_SELF.UpdateContentItemInList( #UpdateItem )
    
    Else
    
    * Its new so add to the top of the list with "New" instead of size
    #COM_SELF.AddContentItemToList( #UpdateItem True )
    
    Endif
    
    #COM_SELF.SetListActionAfterLoad
    
    Endroutine
    
    Mthroutine Name(HandleRemoveItemInList)
    Define_Map For(*INPUT) Class(#BPFContentLibraryItemClass) Name(#DeletedItem) Pass(*BY_REFERENCE)
    
    * Look for the entry number
    If (#ContentItemList.FindReference( #DeletedItem ) *IsNot *Null)
    Dlt_Entry Number(#ContentItemList.FindReference( #DeletedItem ).Entry) From_List(#ContentItemList)
    Endif
    
    #COM_SELF.SetListActionAfterLoad
    
    Endroutine
    
    Mthroutine Name(AddContentItemToList) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#BPFContentLibraryItemClass) Name(#Item) Pass(*BY_REFERENCE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#IsUpdatedItem) Mandatory(False)
    
    * Add new Item to list
    Add_Entry To_List(#ContentItemList)
    
    * If an add make it top
    If (#IsUpdatedItem)
    #ContentItemList.CurrentItem.Position := 1
    Endif
    
    * Bind the data to the item
    #ContentItemList.CurrentItem.RelatedReference <= #Item
    
    * Set the account icon
    #ContentItemIcon.CurrentItem.Icon := #Item.ItemTypeIcon
    #ContentItemIcon.CurrentItem.ThemeDrawStyle := #Item.ItemTypeColour
    
    * Set Primary & Secondary Text
    #ContentItemPrimaryText.CurrentItem.Caption := #Item.ItemName
    
    * Set Type Text
    #ContentItemSizeText.CurrentItem.Visible := (#SYS_WEB.Device = Desktop)
    
    If (#Item.ItemType = YTV)
    
    #ContentItemSecondaryText.CurrentItem.Caption := 'ID ' + #Item.ItemToken
    
    #ContentItemSizeText.CurrentItem.Caption := #Item.ItemDuration
    
    Else
    
    #ContentItemSecondaryText.CurrentItem.Caption := #Item.ItemToken
    
    If (#Item.ItemSize<KB> < 1024)
    
    #ContentItemSizeText.CurrentItem.Caption := #Item.ItemSize<KB>.asstring + ' KB'
    
    Else
    
    #ContentItemSizeText.CurrentItem.Caption := #Item.ItemSize<MB>.asstring + ' MB'
    
    Endif
    
    Endif
    
    If (#IsUpdatedItem)
    
    #ContentItemSizeText.CurrentItem.Caption := #STRINGS.Text<'CONTENTITEMSIZETEXT.CURRENTITEM.CAPTION.NEW'>
    
    Endif
    
    Endroutine
    
    Mthroutine Name(UpdateContentItemInList) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#BPFContentLibraryItemClass) Name(#Item) Pass(*BY_REFERENCE)
    
    * Look for the entry number
    Get_Entry Number(#ContentItemList.FindReference( #Item ).Entry) From_List(#ContentItemList)
    
    * If we find, we update
    If_Status Is(*OKAY)
    
    * Set the account icon
    #ContentItemIcon.CurrentItem.Icon := #Item.ItemTypeIcon
    #ContentItemIcon.CurrentItem.ThemeDrawStyle := #Item.ItemTypeColour
    
    * Set Primary & Secondary Text
    #ContentItemPrimaryText.CurrentItem.Caption := #Item.ItemName
    
    * Set Type Text
    #ContentItemSizeText.CurrentItem.Visible := (#SYS_WEB.Device = Desktop)
    
    If (#Item.ItemType = YTV)
    
    #ContentItemSecondaryText.CurrentItem.Caption := 'ID ' + #Item.ItemToken
    
    #ContentItemSizeText.CurrentItem.Caption := #Item.ItemDuration
    
    Else
    
    #ContentItemSecondaryText.CurrentItem.Caption := #Item.ItemToken
    
    If (#Item.ItemSize<KB> < 1024)
    
    #ContentItemSizeText.CurrentItem.Caption := #Item.ItemSize<KB>.asstring + ' KB'
    
    Else
    
    #ContentItemSizeText.CurrentItem.Caption := #Item.ItemSize<MB>.asstring + ' MB'
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetListActionForLoading) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#ClearList) Mandatory(True)
    
    If (#ClearList)
    
    * Clear
    Clr_List Named(#ContentItemList)
    
    Endif
    
    #COM_SELF.SetListActionDisplay( True *NULL *NULL LOAD )
    
    Endroutine
    
    Mthroutine Name(SetListActionAfterLoad)
    
    * If we have more results to load
    If (#ContentItemList.Items.ItemCount < #Sharing.ContentLibrary.ItemCount)
    
    * We show loading more
    #COM_SELF.SetListActionDisplay( True #LoadMoreDisplayStyle ForegroundMediumPrimary MORE )
    
    Endif
    
    * if we have no more results to load
    If (#ContentItemList.Items.ItemCount = #Sharing.ContentLibrary.ItemCount)
    
    * Do we have more results on the server
    If (#MoreResults)
    
    * We show Refine search
    #COM_SELF.SetListActionDisplay( True *NULL *NULL LIMIT )
    
    Else
    
    If (#ContentItemList.Items.ItemCount > 0)
    
    * No more results on server, so no message needed
    #ContentItemListActionLabel.Visible := False
    
    Else
    
    * No Results found
    #COM_SELF.SetListActionDisplay( True *NULL *NULL EMPTY )
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetListActionDisplay) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#Visible)
    Define_Map For(*INPUT) Class(#prim_vs.Style) Name(#Style) Mandatory(*NULL) Pass(*BY_REFERENCE)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#DrawStyle) Mandatory(*NULL)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Message) Mandatory(*NULL)
    
    If (#Visible)
    
    * Set Style
    #ContentItemListActionLabel.Style <= #Style
    #ContentItemListActionLabel.ThemeDrawStyle := #DrawStyle
    
    * Set Text
    #ContentItemListActionLabel := #STRINGS.Text<('CONTENTITEMLISTACTIONLABEL.CAPTION.' + #Message.UpperCase)>
    #ContentItemListActionLabel.ComponentTag := #Message.UpperCase
    
    * Ensure we can see Action Label
    #ContentItemListActionLabel.Visible := True
    
    Else
    
    * If not visible then just hide
    #ContentItemListActionLabel.Visible := False
    
    Endif
    
    Endroutine
    
    Mthroutine Name(HandleAddSelection)
    
    #Menu <= (*New #BPFContentLibraryAddItemSelection)
    #Menu.Open( #AddNewContentButton #Strings )
    
    Endroutine
    
    Evtroutine Handling(#Menu.FileUploadClicked)
    
    #Navigate.To Eventname(CONTENTITEMFILEADDVIEW)
    
    Endroutine
    
    Evtroutine Handling(#Menu.YouTubeClicked)
    
    #Navigate.To Eventname(CONTENTITEMYOUTUBEADDVIEW)
    
    Endroutine
    
    Evtroutine Handling(#Menu.Closed)
    
    #Menu <= *NULL
    
    Endroutine
    
    End_Com
