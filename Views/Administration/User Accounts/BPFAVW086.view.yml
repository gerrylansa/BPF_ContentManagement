Web View:
  Name:               BPFAdminUserSetContentShareView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal Admin User Set Content Share View"

  - JPN:
      Description:        "Portal Admin User Set Content Share View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Componenttag('BPFAPP008')
    
    * Reference Application Objects
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFAdminUserSetContentShareViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFUserAccountAdminDataClient) Name(#UserAccounts) Reference(*DYNAMIC)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(93) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#MainLayout)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#ControlPanel) Parent(#MainLayout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(24)
    
    
    * Panel Control Layout
    Define_Com Class(#PRIM_TBLO) Name(#ControlLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row1) Displayposition(1) Parent(#ControlLayout) Units(Content) Height(139)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row2) Displayposition(2) Parent(#ControlLayout) Units(Pixels) Height(62)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ControlColumn1) Displayposition(1) Parent(#ControlLayout) Minwidth(44)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ControlColumn2) Displayposition(2) Parent(#ControlLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#ControlColumn3) Displayposition(3) Parent(#ControlLayout) Minwidth(44)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#ControlLayoutItem1) Alignment(CenterRight) Column(#ControlColumn2) Manage(#PanelAction) Parent(#ControlLayout) Row(#Layout1Row2) Sizing(None)
    
    * Controls
    Define_Com Class(#PRIM_TBLO.Item) Name(#ControlLayoutItem4) Alignment(TopLeft) Column(#ControlColumn2) Manage(#ControlPanelHeading) Parent(#ControlLayout) Row(#Layout1Row1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#ControlLayoutItem5) Alignment(TopLeft) Column(#ControlColumn2) Manage(#ContentShareSelection) Parent(#ControlLayout) Row(#Layout1Row1) Sizing(FitToWidth) Flow(Down) Margintop(16)
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Caption('Change Content Share') Displayposition(1) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(2) Height(38) Left(350) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('To share content with the user select one of the content shares and then click save. Once selected the user will see that content has been shared with them when they next sign in to the portal.')
    Define_Com Class(#PRIM_PANL) Name(#ControlPanel) Displayposition(3) Height(203) Left(410) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(141) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#ControlLayout)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#PanelAction) Caption('Save') Displayposition(3) Left(734) Parent(#ControlPanel) Tabposition(2) Themedrawstyle('ForegroundMediumPrimary') Top(152) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#ControlPanelHeading) Displayposition(1) Left(44) Parent(#ControlPanel) Tabposition(3) Themedrawstyle('Heading3') Top(24) Width(790) Caption('Content Shares') Height(24) Wordwrap(True)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#ContentShareSelection) Caption('Select a share') Displayposition(2) Left(44) Parent(#ControlPanel) Tabposition(1) Top(64) Width(790)
    
    * References
    Define_Com Class(#BPFUserAccountAdminItem) Name(#CurrentAccount) Reference(*DYNAMIC)
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get References
    #UserAccounts <= (#Data.GetDataClient( #COM_SELF ) *As #BPFUserAccountAdminDataClient)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get Reference to current user
    #CurrentAccount <= #UserAccounts.UserAccount<#SYS_WEB.URLParameters<VALUE>.value.UpperCase>
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #COM_SELF.SetStrings
    
    If (#CurrentAccount *IsNot *NULL)
    
    #UserAccounts.GetShares
    
    Else
    
    #UserAccounts.GetUserAccountsByID( #SYS_WEB.URLParameters<VALUE>.value )
    
    Endif
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Action
    Evtroutine Handling(#PanelAction.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    
    #CurrentAccount.SetContentShare( #ContentShareSelection.Value )
    
    Endroutine
    
    * Data
    Evtroutine Handling(#UserAccounts.OnGetUserAccountCompleted)
    
    * Get Reference to current user
    #CurrentAccount <= #UserAccounts.UserAccount<#SYS_WEB.URLParameters<VALUE>.value>
    #UserAccounts.GetShares
    
    Endroutine
    
    Evtroutine Handling(#UserAccounts.OnGetUserAccountFailed)
    
    * Show error
    #BPFPortal.ShowToaster( #STRINGS.Text<'USERACCOUNTS.ONGETUSERACCOUNTFAILED'> 7500 )
    
    * Turn off loading
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#UserAccounts.OnGetContentSharesCompleted) Contentshares(#Shares)
    
    #COM_SELF.BuildOptions( #Shares )
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#UserAccounts.OnGetContentSharesFailed)
    
    * Show error
    #BPFPortal.ShowToaster( #STRINGS.Text<'USERACCOUNTS.ONGETCONTENTSHARESFAILED'> 7500 )
    
    * Turn off loading
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#CurrentAccount.OnSetContentShareCompleted)
    
    #BPFPortal.ShowToaster( #STRINGS.Text<'CURRENTACCOUNT.ONSETCONTENTSHARECOMPLETED'> )
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    Evtroutine Handling(#CurrentAccount.OnSetContentShareFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<'CURRENTACCOUNT.ONSETCONTENTSHAREFAILED'> 7500 )
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #PANELHEADING.CAPTION := #STRINGS.Text<PANELHEADING.CAPTION>
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    #CONTROLPANELHEADING.CAPTION := #STRINGS.Text<CONTROLPANELHEADING.CAPTION>
    #CONTENTSHARESELECTION.CAPTION := #STRINGS.Text<'CONTENTSHARESELECTION.CAPTION'>
    #PANELACTION.CAPTION := #STRINGS.Text<PANELACTION.CAPTION>
    
    Endroutine
    
    Mthroutine Name(BuildOptions) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_LCOL<#BPFContentShareClass>) Name(#ContentShares) Pass(*BY_REFERENCE)
    
    #ContentShareSelection.Items.Add Caption(#STRINGS.Text<'CONTENTSHARESELECTION.DEFAULT'>) Value(NOSHARE)
    
    For Each(#Item) In(#ContentShares)
    
    #ContentShareSelection.Items.Add Caption(#Item.ShareName) Value(#Item.Identifier)
    
    Endfor
    
    Endroutine
    
    End_Com
