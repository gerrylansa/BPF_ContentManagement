Web View:
  Name:               BPFUserSecuritySetQuestionView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal User Security Set Q & A"

  - JPN:
      Description:        "Portal User Security Set Q & A"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Verticalscroll(True)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFUserSecuritySetQuestionViewStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(76) Units(Content)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow1) Displayposition(2) Parent(#MainLayout) Height(304) Units(Content)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#SecurityDetailsEntryPanel) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(24)
    
    * Enabled Panel Layout
    Define_Com Class(#PRIM_TBLO) Name(#NewPasswordEntryPanelLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#NewPasswordEntryPanelLayoutRow1) Displayposition(1) Parent(#NewPasswordEntryPanelLayout) Units(Content) Height(222)
    Define_Com Class(#PRIM_TBLO.Row) Name(#NewPasswordEntryPanelLayoutRow2) Displayposition(2) Parent(#NewPasswordEntryPanelLayout) Units(Pixels) Height(56)
    Define_Com Class(#PRIM_TBLO.Column) Name(#NewPasswordEntryPanelLayoutColumn1) Displayposition(1) Parent(#NewPasswordEntryPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#NewPasswordEntryPanelLayoutColumn2) Displayposition(2) Parent(#NewPasswordEntryPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#PasswordEntryPanelLayoutColumn3) Displayposition(3) Parent(#NewPasswordEntryPanelLayout) Minwidth(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem1) Manage(#SecurityDetailsPanelHeading) Parent(#NewPasswordEntryPanelLayout) Row(#NewPasswordEntryPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#NewPasswordEntryPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(24) Marginbottom(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem3) Column(#NewPasswordEntryPanelLayoutColumn1) Manage(#Divider) Parent(#NewPasswordEntryPanelLayout) Sizing(FitToWidth) Row(#NewPasswordEntryPanelLayoutRow2) Alignment(TopLeft) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem4) Alignment(CenterRight) Column(#NewPasswordEntryPanelLayoutColumn2) Manage(#SecurityDetailsAction) Parent(#NewPasswordEntryPanelLayout) Row(#NewPasswordEntryPanelLayoutRow2) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem5) Alignment(TopLeft) Column(#NewPasswordEntryPanelLayoutColumn2) Manage(#ConfirmAnswer) Parent(#NewPasswordEntryPanelLayout) Row(#NewPasswordEntryPanelLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(8)
    
    
    * Main Controls
    Define_Com Class(#PRIM_TBLO.Item) Name(#NewPasswordEntryPanelLayoutItem1) Alignment(TopLeft) Column(#NewPasswordEntryPanelLayoutColumn2) Manage(#SecurityQuestionSelection) Parent(#NewPasswordEntryPanelLayout) Row(#NewPasswordEntryPanelLayoutRow1) Sizing(FitToWidth) Flow(Down)
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Displayposition(1) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Caption('Change Account Security') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(2) Height(21) Left(350) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('To secure your myPortal account select a security question and provide a matching answer. We will ask you this question if we need to confirm you crenditials')
    
    * Password Entry Panel & Controls
    Define_Com Class(#PRIM_PANL) Name(#SecurityDetailsEntryPanel) Displayposition(3) Height(280) Left(410) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(124) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#NewPasswordEntryPanelLayout)
    Define_Com Class(#PRIM_MD.Label) Name(#SecurityDetailsPanelHeading) Caption('Account Security') Displayposition(1) Left(24) Parent(#SecurityDetailsEntryPanel) Tabposition(1) Themedrawstyle('Heading3') Top(24) Width(830) Wordwrap(True) Height(24)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#SecurityQuestionSelection) Caption('Select a question') Displayposition(2) Left(24) Parent(#SecurityDetailsEntryPanel) Tabposition(2) Top(64) Width(830) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Edit) Name(#ConfirmAnswer) Caption('Your Answer') Displayposition(4) Left(24) Parent(#SecurityDetailsEntryPanel) Tabposition(3) Top(147) Width(830)
    
    Define_Com Class(#PRIM_PANL) Name(#Divider) Displayposition(5) Height(8) Left(0) Parent(#SecurityDetailsEntryPanel) Tabposition(5) Tabstop(False) Themedrawstyle('TopDivider') Top(222) Width(878)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#SecurityDetailsAction) Caption('CHANGE') Displayposition(3) Left(754) Parent(#SecurityDetailsEntryPanel) Tabposition(4) Themedrawstyle('ForegroundMediumPrimary') Top(232) Style(#NavigationLinkStyle)
    
    * References
    Define_Com Class(#BPFUserAccountClass) Name(#CurrentUser) Reference(*DYNAMIC)
    
    * Routines
    
    * Events
    
    * Self
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get Reference to logging data client
    #CurrentUser <= #Session.CurrentUser
    
    * Check verification
    If (#CurrentUser.AccountVerified)
    
    * Pass Check to get here, so invalidate as the check will need to be done again
    #CurrentUser.AccountVerified := False
    
    Else
    
    * Did not pass the check, if we got here someone is hacking the site
    #SYS_WEB.History.GoBack
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    #COM_SELF.SetStrings
    
    #Session.GetSecurityQuestions
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Prepare)
    
    #Navigate.To Eventname(VERIFYUSERACCOUNT) Value(USERSECURITYCHANGESECURITY) Type(REPLACE) Signalreplaceevent(False)
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Data
    * Session
    Evtroutine Handling(#Session.OnGetSecurityQuestionCompleted) Questions(#Questions)
    
    #COM_SELF.BuildQuestionList( #Questions )
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnGetSecurityQuestionFailed)
    
    #COM_SELF.HandleListFailure
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#CurrentUser.OnSetSecurityCompleted)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    * All Okay so head back to settings screen
    #BPFPortal.ShowToaster( #STRINGS.Text<CURRENTUSER.ONSETSECURITYCOMPLETED> )
    #SYS_WEB.History.GoBack
    
    
    Endroutine
    
    Evtroutine Handling(#CurrentUser.OnSetSecurityFailed)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    #BPFPortal.ShowToaster( #STRINGS.Text<CURRENTUSER.ONSETSECURITYFAILED> 7500 )
    
    Endroutine
    
    * Actions
    Evtroutine Handling(#SecurityDetailsAction.Click #SecurityQuestionSelection.Enter #ConfirmAnswer.Enter)
    
    If (#COM_SELF.CheckDataIsValid)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #CurrentUser.SetSecurityDetails( #SecurityQuestionSelection #ConfirmAnswer.Value )
    
    Endif
    
    Endroutine
    
    * Field Events
    Evtroutine Handling(#SecurityQuestionSelection.Changed #ConfirmAnswer.Changed) Com_Sender(#Sender)
    
    #Sender.ClearError
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #PANELHEADING.CAPTION := #STRINGS.Text<PANELHEADING.CAPTION>
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    #SECURITYDETAILSPANELHEADING.CAPTION := #STRINGS.Text<SECURITYDETAILSPANELHEADING.CAPTION>
    #SECURITYQUESTIONSELECTION.CAPTION := #STRINGS.Text<SECURITYQUESTIONSELECTION.CAPTION>
    #CONFIRMANSWER.CAPTION := #STRINGS.Text<CONFIRMANSWER.CAPTION>
    #SECURITYDETAILSACTION.CAPTION := #STRINGS.Text<SECURITYDETAILSACTION.CAPTION>
    
    Endroutine
    
    * Build Question List
    Mthroutine Name(BuildQuestionList)
    
    * Map In Questions
    Define_Map For(*INPUT) Class(#PRIM_LCOL<#BPFAlphaValuesItemClass>) Name(#QuestionsList) Pass(*BY_REFERENCE)
    
    * Ensure List is Clear
    #SecurityQuestionSelection.Items.RemoveAll
    
    * Build List
    For Each(#Question) In(#QuestionsList)
    
    #SecurityQuestionSelection.Items.Add Value(#Question.Value) Caption(#Question.Value)
    
    Endfor
    
    Endroutine
    
    * Validation of Data
    Mthroutine Name(CheckDataIsValid)
    Define_Map For(*RESULT) Class(#PRIM_BOLN) Name(#Result)
    
    * Assume Success
    #Result := True
    #Com_self.ClearAllErrors
    
    If (#SecurityQuestionSelection.Value = *BLANK)
    
    #SecurityQuestionSelection.ShowError( #STRINGS.Text<SECURITYQUESTIONSELECTION.BLANK> )
    
    Endif
    
    If (#ConfirmAnswer.Value = *BLANK)
    
    #ConfirmAnswer.ShowError( #STRINGS.Text<CONFIRMANSWER.BLANK> )
    
    Endif
    
    #Result := ((#SecurityQuestionSelection.value <> *blank) *And (#ConfirmAnswer.Value <> *Blank))
    
    Endroutine
    
    
    Mthroutine Name(ClearAllErrors) Access(*PRIVATE)
    
    * Check if we have any errors
    For Each(#Control) In(#COM_SELF.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.Edit)
    
    If (#Control.HasError)
    
    #Control.ClearError
    
    Endif
    
    Endfor
    
    Endroutine
    
    Mthroutine Name(HandleListFailure)
    
    #SecurityQuestionSelection.ShowError( #STRINGS.Text<SECURITYQUESTIONSELECTION.NOENTRIES> )
    
    Endroutine
    
    End_Com
