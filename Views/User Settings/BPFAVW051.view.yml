Web View:
  Name:               BPFUserSecurityVerificationView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal User Verification by Security"

  - JPN:
      Description:        "Portal User Verification by Security"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Width(1700) Height(800) Layoutmanager(#MainLayout) Verticalscroll(True)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFUserSecurityVerificationViewStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Main View Layout
    Define_Com Class(#PRIM_TBLO) Name(#MainLayout) Margintop(24)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(2) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(3) Parent(#MainLayout) Width(96) Minwidth(280) Maxwidth(880)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn4) Displayposition(4) Parent(#MainLayout) Maxwidth(60)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn5) Displayposition(5) Parent(#MainLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#MainLayout) Height(76) Units(Content)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainLayoutRow1) Displayposition(2) Parent(#MainLayout) Height(242) Units(Content)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#PanelHeading) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem2) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#InstructionText) Parent(#MainLayout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Margintop(24) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainLayoutItem3) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#VerificationEntryPanel) Parent(#MainLayout) Row(#MainLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(24)
    
    * Enabled Panel Layout
    Define_Com Class(#PRIM_TBLO) Name(#PasswordEntryPanelLayout) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#PasswordEntryPanelLayoutRow1) Displayposition(1) Parent(#PasswordEntryPanelLayout) Units(Content) Height(160)
    Define_Com Class(#PRIM_TBLO.Row) Name(#PasswordEntryPanelLayoutRow2) Displayposition(2) Parent(#PasswordEntryPanelLayout) Units(Pixels) Height(56)
    Define_Com Class(#PRIM_TBLO.Column) Name(#PasswordEntryPanelLayoutColumn1) Displayposition(1) Parent(#PasswordEntryPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#PasswordEntryPanelLayoutColumn2) Displayposition(2) Parent(#PasswordEntryPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#PasswordEntryPanelLayoutColumn3) Displayposition(3) Parent(#PasswordEntryPanelLayout) Minwidth(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#ControlLayoutItem6) Manage(#PasswordEntryPanelHeading) Parent(#PasswordEntryPanelLayout) Row(#PasswordEntryPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#PasswordEntryPanelLayoutColumn2) Alignment(TopLeft) Flow(Down) Margintop(24) Marginbottom(16)
    
    
    * Layout Items
    
    * Main Controls
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem1) Alignment(TopLeft) Column(#PasswordEntryPanelLayoutColumn2) Manage(#SecurityAnswerEdit) Parent(#PasswordEntryPanelLayout) Row(#PasswordEntryPanelLayoutRow1) Sizing(FitToWidth) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem2) Column(#PasswordEntryPanelLayoutColumn1) Manage(#Divider) Parent(#PasswordEntryPanelLayout) Sizing(FitToWidth) Row(#PasswordEntryPanelLayoutRow2) Alignment(TopLeft) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem3) Alignment(CenterRight) Column(#PasswordEntryPanelLayoutColumn2) Manage(#PasswordEntryAction) Parent(#PasswordEntryPanelLayout) Row(#PasswordEntryPanelLayoutRow2) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PasswordEntryPanelLayoutItem4) Alignment(TopLeft) Column(#PasswordEntryPanelLayoutColumn2) Manage(#SecurityQuestionLabel) Parent(#PasswordEntryPanelLayout) Row(#PasswordEntryPanelLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Marginbottom(8)
    
    Define_Com Class(#PRIM_MD.Label) Name(#PanelHeading) Displayposition(1) Left(350) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading1') Top(24) Width(1000) Icon('arrow_back') Captionmarginleft(24) Height(31) Wordwrap(True) Caption('&1') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#InstructionText) Captionalignment(TopLeft) Displayposition(2) Height(21) Left(350) Parent(#COM_OWNER) Tabposition(2) Top(79) Width(1000) Wordwrap(True) Caption('In order to change this account, we need to make sure it is you changing it. So please enter your security answer below to verify that it is you')
    
    * Password Entry Panel & Controls
    Define_Com Class(#PRIM_PANL) Name(#VerificationEntryPanel) Displayposition(3) Height(218) Left(410) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(124) Width(880) Themedrawstyle('Card') Style(#RoundedCornerStyle) Layoutmanager(#PasswordEntryPanelLayout)
    Define_Com Class(#PRIM_MD.Label) Name(#PasswordEntryPanelHeading) Caption('Verification') Displayposition(1) Left(24) Parent(#VerificationEntryPanel) Tabposition(1) Themedrawstyle('Heading2') Top(24) Width(830) Wordwrap(True) Height(27)
    Define_Com Class(#PRIM_MD.Edit) Name(#SecurityAnswerEdit) Caption('Enter your answer') Displayposition(3) Left(24) Parent(#VerificationEntryPanel) Tabposition(2) Top(85) Width(830)
    Define_Com Class(#PRIM_PANL) Name(#Divider) Displayposition(4) Height(8) Left(0) Parent(#VerificationEntryPanel) Tabposition(3) Tabstop(False) Themedrawstyle('TopDivider') Top(160) Width(878)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#PasswordEntryAction) Caption('VERIFY') Displayposition(5) Left(754) Parent(#VerificationEntryPanel) Tabposition(4) Themedrawstyle('ForegroundMediumPrimary') Top(170) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#SecurityQuestionLabel) Displayposition(2) Left(24) Parent(#VerificationEntryPanel) Tabposition(5) Top(67) Height(10) Width(830)
    
    * Variables
    Define_Com Class(#PRIM_ALPH) Name(#RequestedFor)
    
    * References
    Define_Com Class(#BPFUserAccountClass) Name(#CurrentUser) Reference(*DYNAMIC)
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get Reference to logging data client
    #CurrentUser <= #Session.CurrentUser
    
    * Get Requested for
    #RequestedFor := #SYS_WEB.URLParameters<VALUE>.value
    #COM_SELF.SetDisplay
    
    * Get Security question
    #Session.GetSecurityCheckQuestion
    
    Endroutine
    
    * Navigation
    Evtroutine Handling(#PanelHeading.Click)
    
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Data
    Evtroutine Handling(#Session.OnGetSecurityCheckQuestionCompleted) Question(#Question)
    
    #SecurityQuestionLabel := #Question
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnGetSecurityCheckQuestionFailed)
    
    #SYS_WEB.History.GoBack
    #BPFPortal.ShowToaster( #STRINGS.Text<SESSION.ONGETSECURITYCHECKQUESTIONFAILED> )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnCheckSecurityDetailsCompleted)
    
    #CurrentUser.AccountVerified := True
    #Navigate.To Eventname(#RequestedFor) Type(REPLACE) Signalreplaceevent(True)
    
    Endroutine
    
    Evtroutine Handling(#Session.OnCheckSecurityDetailsFailed) Reason(#Reason)
    
    #BPFPortal.ViewsPanel.SetWaitingState( OFF )
    
    If (#Reason <> NV)
    
    #SecurityAnswerEdit.ShowError( #STRINGS.Text<SESSION.ONVERIFYUSERACCOUNTFAILED> )
    
    Else
    
    #Navigate.BackToTarget( INITIAL )
    
    Endif
    
    Endroutine
    
    
    * Actions
    Evtroutine Handling(#PasswordEntryAction.Click #SecurityAnswerEdit.Enter)
    
    If (#SecurityAnswerEdit <> *BLANK)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #Session.CheckSecuirtyDetails( #SecurityAnswerEdit.Value )
    
    Else
    
    #SecurityAnswerEdit.ShowError( #STRINGS.Text<SECURITYANSWEREDIT.BLANK> )
    
    Endif
    
    Endroutine
    
    * Field Events
    Evtroutine Handling(#SecurityAnswerEdit.Changed) Com_Sender(#Sender)
    
    If (#Sender.HasError)
    
    #Sender.ClearError
    
    Endif
    
    Endroutine
    
    * Methods
    Mthroutine Name(SetDisplay) Access(*PRIVATE)
    
    Case Of_Field(#RequestedFor.UpperCase)
    
    When Value_Is(= USERSECURITYREMOVEACCOUNT)
    
    #PanelHeading.Caption := #STRINGS.Text<PANELHEADING.CAPTION.REMOVEACCOUNT>
    
    Endcase
    
    #PASSWORDENTRYPANELHEADING.CAPTION := #STRINGS.Text<PASSWORDENTRYPANELHEADING.CAPTION>
    #INSTRUCTIONTEXT.CAPTION := #STRINGS.Text<INSTRUCTIONTEXT.CAPTION>
    #SECURITYANSWEREDIT.CAPTION := #STRINGS.Text<SECURITYANSWEREDIT.CAPTION>
    #PASSWORDENTRYACTION.CAPTION := #STRINGS.Text<PASSWORDENTRYACTION.CAPTION>
    
    Endroutine
    
    End_Com
