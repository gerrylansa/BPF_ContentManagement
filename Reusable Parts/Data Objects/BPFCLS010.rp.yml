Reusable Part:
  Name:               BPFEmailTemplateDataClass
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Email Template Data Class"

  - JPN:
      Description:        "Email Template Data Class"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_OBJT *LISTFIELDS #ListFields)
    
    * Map Data for List Processing
    Group_By Name(#ListFields) Fields(#BPFCodeGroup #BPFCodeAlphaValue)
    
    * References
    
    * Global References
    Define_Com Class(#BPFBrowserEvents) Name(#Browser) Scope(*APPLICATION)
    
    * Defintions
    
    * Properties
    Define_Pty Name(TemplateID) Get(*AUTO #BPFCodeGroup)
    Define_Pty Name(DisplayName) Get(*AUTO #BPFCodeAlphaValue)
    Define_Pty Name(Properties) Get(*REFERENCE #Properties)
    Define_Pty Name(PropertyValue) Get(GetPropertyValue) Set(SetPropertyValue)
    Define_Pty Name(Body) Get(*AUTO #BodyHTML)
    Define_Pty Name(File) Set(*AUTO #BPFGenericBlob)
    
    * Events
    Define_Evt Name(OnLoadCompleted)
    Define_Evt Name(OnLoadFailed)
    
    Define_Evt Name(OnSaveCompleted)
    Define_Evt Name(OnSaveFailed)
    
    * Collections
    Define_Com Class(#PRIM_LCOL<#BPFAlphaCodeGroupClass>) Name(#Properties)
    
    * Variables
    Define_Com Class(#PRIM_WEB.ClobReader) Name(#Reader)
    Define_Com Class(#PRIM_DC.UnicodeString) Name(#BodyHTML)
    * Rotuines
    
    * Properties
    Ptyroutine Name(GetPropertyValue)
    Define_Map For(*OUTPUT) Class(#PRIM_ALPH) Name(#Property)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Key)
    
    For Each(#Item) In(#Properties)
    
    Continue If(#Item.CodeName.UpperCase <> #Key.UpperCase)
    
    #Property := #Item.Value
    
    Leave
    
    Endfor
    
    Endroutine
    
    Ptyroutine Name(SetPropertyValue)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Property)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Key)
    
    For Each(#Item) In(#Properties)
    
    Continue If(#Item.CodeName.UpperCase <> #Key.UpperCase)
    
    #Item.Value := #Property
    
    Leave
    
    Endfor
    
    Endroutine
    
    * Events
    Evtroutine Handling(#Reader.Completed) Data(#Data)
    
    #BodyHTML := #Data
    
    * Signal
    Signal Event(OnLoadCompleted)
    
    Endroutine
    
    * Methods
    Mthroutine Name(Load)
    Define_Map For(*INPUT) Class(#PRIM_DC.UnicodeString) Name(#ForIsoCode)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFEmailSettingsDataServer.GetTemplateData) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Forisocode(#ForIsoCode) Fortemplate(#BPFCodeGroup) Properties(#Properties) Template(#BPFGenericCLOB) Status(#Status)
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    * Convert
    #Reader.Read( #BPFGenericCLOB )
    
    Else
    
    Signal Event(OnLoadFailed)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnLoadFailed)
    
    Endroutine
    
    Endroutine
    
    Mthroutine Name(Save)
    Define_Map For(*INPUT) Class(#PRIM_DC.UnicodeString) Name(#ForIsoCode)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFEmailSettingsDataServer.SaveTemplateData) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Forisocode(#ForIsoCode) Fortemplate(#BPFCodeGroup) Properties(#Properties) Newtemplate(#BPFGenericBlob) Status(#Status)
    
    * Clear the temp BLOB
    #BPFGenericBlob := *SQLNULL
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    #BPFCodeAlphaValue := #Properties<1>.Value
    Signal Event(OnSaveCompleted)
    
    Else
    
    Signal Event(OnSaveFailed)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnSaveFailed)
    
    Endroutine
    
    Endroutine
    
    Mthroutine Name(Download)
    
    #Browser.Download( #BPFGenericCLOB #com_self.PropertyValue<BODY_TEMPLATE> )
    
    Endroutine
    
    End_Com
