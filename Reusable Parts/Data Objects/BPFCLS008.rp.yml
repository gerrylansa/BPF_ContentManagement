Reusable Part:
  Name:               BPFBooleanCodeGroupClass
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "System code group with Boolen Function"

  - JPN:
      Description:        "System code group with Boolen Function"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_OBJT *LISTFIELDS #ListFields)
    
    * Group by for data mapping of listfields
    Group_By Name(#Listfields) Fields(#BPFCodeGroup #BPFCodeName #BPFCodeSequence #BPFCodeAlphaValue #BPFCodeNumericValue)
    
    * Defintions
    
    * Properties
    Define_Pty Name(CodeGroup) Get(*AUTO #BPFCodeGroup)
    Define_Pty Name(CodeName) Get(*AUTO #BPFCodeName)
    Define_Pty Name(Sequence) Get(*AUTO #BPFCodeSequence)
    Define_Pty Name(Text) Get(*AUTO #BPFCodeAlphaValue)
    Define_Pty Name(Value) Get(GetBooleanValue) Set(SetBooleanValue)
    
    * Events
    Define_Evt Name(OnSaveCompleted)
    Define_Evt Name(OnSaveFailed)
    
    * Routines
    
    * Properties
    Ptyroutine Name(GetBooleanValue)
    Define_Map For(*OUTPUT) Class(#PRIM_BOLN) Name(#Property)
    
    #Property := #BPFCodeNumericValue.AsBoolean
    
    Endroutine
    
    Ptyroutine Name(SetBooleanValue)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#Property)
    
    #BPFCodeNumericValue := #Property.AsNumber
    
    Endroutine
    
    * Events
    
    * Methods
    Mthroutine Name(Save)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFSystemCodesDataServer.SaveNumericCode) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Settinggroup(#BPFCodeGroup) Settingname(#BPFCodeName) Settingsequence(#BPFCodeSequence) Settovalue(#BPFCodeNumericValue) Status(#Status)
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    Signal Event(OnSaveCompleted)
    
    Else
    
    Signal Event(OnSaveFailed)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnSaveFailed)
    
    Endroutine
    
    Endroutine
    
    End_Com
