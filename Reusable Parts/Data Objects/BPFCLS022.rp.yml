Reusable Part:
  Name:               BPFContentShareItemClass
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Content Share Item Class"

  - JPN:
      Description:        "Content Share Item Class"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_OBJT *LISTFIELDS #ListFields)
    
    * Appilcation references
    Define_Com Class(#BPFBrowserEvents) Name(#Browser) Scope(*APPLICATION)
    
    * Group by for data mapping of listfields
    Group_By Name(#Listfields) Fields(#BPFContentShareItemID #BPFContentShareItemName #BPFContentItemName #BPFContentShareItemIsFolder #BPFContentShareItemParentID #BPFContentItemTokenFileName #BPFContentItemAlphaDetail #BPFContentItemNumericDetail #BPFContentShareItemContentID #BPFContentShareItemCreatedBy #BPFContentShareItemCreated #BPFContentShareItemModifiedBy #BPFContentShareItemModified)
    
    Group_By Name(#ExtraDetailGroup) Fields(#BPFContentItemDesc #BPFContentItemCreated #BPFContentItemModified #BPFSessionEventValue1 #BPFSessionEventValue2 #BPFSessionEventValue3 #BPFSessionEventValue4)
    
    * Defintions
    
    * Properties
    Define_Pty Name(Identifier) Get(*AUTO #BPFContentShareItemID)
    Define_Pty Name(ItemName) Get(*AUTO #BPFContentShareItemName)
    Define_Pty Name(ContentIdentifier) Get(*AUTO #BPFContentShareItemContentID)
    Define_Pty Name(ContentToken) Get(*AUTO #BPFContentItemTokenFileName)
    Define_Pty Name(ContentName) Get(*AUTO #BPFContentItemName)
    Define_Pty Name(IsFolder) Get(*AUTO #BPFContentShareItemIsFolder)
    Define_Pty Name(FolderCount) Get(GetFoldersCount)
    Define_Pty Name(ContentCount) Get(GetContentCount)
    Define_Pty Name(ItemCount) Get(GetTotalCount)
    Define_Pty Name(Type) Get(GetItemType)
    Define_Pty Name(TypeDescription) Get(GetItemTypeDescription)
    Define_Pty Name(TypeIcon) Get(GetItemTypeIcon)
    Define_Pty Name(TypeColour) Get(GetItemTypeColour)
    Define_Pty Name(ContentSize) Get(GetContentSize)
    Define_Pty Name(ContentDuration) Get(GetContentDuration)
    Define_Pty Name(ItemCreated) Get(*AUTO #BPFContentShareItemCreated)
    Define_Pty Name(ItemModified) Get(*AUTO #BPFContentShareItemModified)
    Define_Pty Name(LastActionedDate) Get(GetLastActionedDate)
    Define_Pty Name(VideoThumbnailURL) Get(GetYoutubeThumbURL)
    
    Define_Pty Name(RelatedViewer) Get(GetItemRelatedViewer)
    
    Define_Pty Name(ParentID) Get(*AUTO #BPFContentShareItemParentID)
    Define_Pty Name(AlphaValue) Get(*AUTO #BPFContentItemAlphaDetail)
    Define_Pty Name(NumericValue) Get(*AUTO #BPFContentItemNumericDetail)
    Define_Pty Name(ItemCreatedByID) Get(*AUTO #BPFContentShareItemCreatedBy)
    Define_Pty Name(ItemModifiedByID) Get(*AUTO #BPFContentItemModifiedBy)
    
    Define_Pty Name(Object) Get(*AUTO #BPFContentItemObject)
    
    * Extra Details - loaded seperately
    Define_Pty Name(ContentDescription) Get(*AUTO #BPFContentItemDesc)
    Define_Pty Name(ItemCreatedName) Get(*AUTO #BPFSessionEventValue1)
    Define_Pty Name(ItemModifiedName) Get(*AUTO #BPFSessionEventValue3)
    Define_Pty Name(ContentCreatedName) Get(*AUTO #BPFSessionEventValue2)
    Define_Pty Name(ContentModifiedName) Get(*AUTO #BPFSessionEventValue4)
    Define_Pty Name(ContentCreated) Get(*AUTO #BPFContentItemCreated)
    Define_Pty Name(ContentModified) Get(*AUTO #BPFContentItemModified)
    
    * References
    Define_Com Class(#BPFContentItemDetailsCollection) Name(#Details)
    
    * Events
    Define_Evt Name(OnGetLoadItemCompleted)
    Define_Evt Name(OnGetLoadItemFailed)
    Define_Map For(*INPUT) Class(#PRIM_DC.UnicodeString) Name(#Reason) Mandatory(*NULL)
    
    Define_Evt Name(OnGetLoadExtraDetailsCompleted)
    Define_Evt Name(OnGetLoadExtraDetailsFailed)
    Define_Map For(*INPUT) Class(#PRIM_DC.UnicodeString) Name(#Reason) Mandatory(*NULL)
    
    Define_Evt Name(OnGetObjectCompleted)
    Define_Evt Name(OnGetObjectFailed)
    
    Define_Evt Name(OnItemRenameCompleted)
    Define_Evt Name(OnItemRenameFailed)
    
    
    * Routines
    
    * Properties
    Ptyroutine Name(GetFoldersCount)
    Define_Map For(*OUTPUT) Class(#PRIM_NMBR) Name(#Property)
    
    #Property := 0
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := #BPFContentItemAlphaDetail.AsNumber
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetContentCount)
    Define_Map For(*OUTPUT) Class(#PRIM_NMBR) Name(#Property)
    
    #Property := 0
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := #BPFContentItemNumericDetail
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetTotalCount)
    Define_Map For(*OUTPUT) Class(#PRIM_NMBR) Name(#Property)
    
    #Property := 0
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := #BPFContentItemAlphaDetail.AsNumber + #BPFContentItemNumericDetail
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetItemType)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := 'FOLDER'
    
    Else
    
    #Property := #BPFContentItemAlphaDetail
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetItemTypeDescription)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := #Details.Type<FOLDER>.TypeDescription
    
    Else
    
    If (#Details.Type<#BPFContentItemAlphaDetail>.TypeDescription.IsNull)
    
    #Property := #BPFContentItemAlphaDetail.UpperCase + ' ' + #Details.TYpe<DEFAULT>.TypeDescription
    
    Else
    
    #Property := #Details.Type<#BPFContentItemAlphaDetail>.TypeDescription
    
    Endif
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetItemTypeIcon)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := #Details.Type<FOLDER>.TypeIcon
    
    Else
    
    If (#Details.Type<#BPFContentItemAlphaDetail>.TypeIcon.IsNull)
    
    #Property := #Details.Type<DEFAULT>.TypeIcon
    
    Else
    
    #Property := #Details.TYpe<#BPFContentItemAlphaDetail>.TypeIcon
    
    Endif
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetItemTypeColour)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    
    If (#BPFContentShareItemIsFolder)
    
    #Property := #Details.Type<FOLDER>.TypeColour<#SYS_WEB.WebPage.ThemeBackground>
    
    Else
    
    If (#Details.Type<#BPFContentItemAlphaDetail>.TypeColour<#SYS_WEB.WebPage.ThemeBackground>.IsNull)
    
    #Property := #Details.Type<DEFAULT>.TypeColour<#SYS_WEB.WebPage.ThemeBackground>
    
    Else
    
    #Property := #Details.Type<#BPFContentItemAlphaDetail>.TypeColour<#SYS_WEB.WebPage.ThemeBackground>
    
    Endif
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetContentSize)
    Define_Map For(*OUTPUT) Class(#PRIM_NMBR) Name(#Property)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Key)
    
    #Property := 0
    If ((#BPFContentShareItemIsFolder.IsFalse) *And (#BPFContentItemAlphaDetail <> YTV))
    
    Case Of_Field(#Key.UpperCase)
    
    When Value_Is(= B)
    
    #Property := #BPFContentItemNumericDetail
    
    When Value_Is(= KB)
    
    #Property := (#BPFContentItemNumericDetail / 1024).Round( Up )
    
    When Value_Is(= MB)
    
    #Property := ((#BPFContentItemNumericDetail / 1024) / 1024).Round( Up )
    
    Otherwise
    
    #Property := #BPFContentItemNumericDetail
    
    Endcase
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetContentDuration)
    Define_Map For(*OUTPUT) Class(#PRIM_ALPH) Name(#Property)
    
    Define_Com Class(#PRIM_NMBR) Name(#Hours)
    Define_Com Class(#PRIM_NMBR) Name(#Minutes)
    Define_Com Class(#PRIM_NMBR) Name(#Seconds)
    
    #Property := '0:00'
    If (#BPFContentItemAlphaDetail = YTV)
    
    #Hours := (#BPFContentItemNumericDetail / 3600).Round( Down )
    #Minutes := (#BPFContentItemNumericDetail / 60).Round( Down ) - (60 * #hours)
    #Seconds := #BPFContentItemNumericDetail.AsFloat.FMod( 60 )
    
    If (#Hours > 0)
    
    #Property := #Hours.AsString + ':' + (#Minutes.AsString).RightAdjust( 2 '0' ) + ':' + (#Seconds.AsString).RightAdjust( 2 '0' )
    
    Else
    
    #Property := #Minutes.AsString + ':' + (#Seconds.AsString).RightAdjust( 2 '0' )
    
    Endif
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetLastActionedDate)
    Define_Map For(*OUTPUT) Class(#PRIM_DAT) Name(#Property)
    
    #Property.AllowSqlNull := True
    #Property := #BPFContentShareItemModified
    
    If (#Property.IsSqlNull)
    
    #Property := #BPFContentShareItemCreated
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetYoutubeThumbURL)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    
    #Property := *Null
    
    If (#BPFContentItemAlphaDetail = YTV)
    
    #Property := 'https://img.youtube.com/vi/' + #BPFContentItemTokenFileName + '/mqdefault.jpg'
    
    Endif
    
    Endroutine
    
    Ptyroutine Name(GetItemRelatedViewer)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    
    If (#BPFContentShareItemIsFolder.IsFalse)
    
    If ((#Details.Type<#BPFContentItemAlphaDetail>.RelatedViewer.IsNull))
    
    #Property := #Details.Type<DEFAULT>.RelatedViewer
    
    Else
    
    If ((#Browser.IsInternetExplorer) *And (#Details.Type<#BPFContentItemAlphaDetail>.DownloadInOldBrowsers))
    
    #Property := #Details.Type<DEFAULT>.RelatedViewer
    
    Else
    
    #Property := #Details.Type<#BPFContentItemAlphaDetail>.RelatedViewer
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    * Events
    
    * Methods
    
    * Public
    
    Mthroutine Name(Load)
    Define_Map For(*INPUT) Class(#PRIM_DC.UnicodeString) Name(#ForItem)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFContentSharingDataServer.GetItemDetails) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Item(#ForItem) Data(#Listfields) Status(#Status)
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    Signal Event(OnGetLoadItemCompleted)
    
    Else
    
    Signal Event(OnGetLoadItemFailed) Reason(NOTALLOWED)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnGetLoadItemFailed) Reason(SESSION)
    
    Endroutine
    
    Endroutine
    
    Mthroutine Name(GetExtraDetails)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFContentSharingDataServer.GetExtraItemDetails) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Item(#BPFContentShareItemID) Data(#ExtraDetailGroup) Status(#Status)
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    Signal Event(OnGetLoadExtraDetailsCompleted)
    
    Else
    
    Signal Event(OnGetLoadExtraDetailsFailed) Reason(#Status)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnGetLoadExtraDetailsFailed)
    
    Endroutine
    
    Endroutine
    
    Mthroutine Name(Rename)
    Define_Map For(*INPUT) Class(#Prim_DC.UnicodeString) Name(#NewName)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFContentSharingDataServer.RenameItem) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Item(#BPFContentShareItemID) Name(#NewName) Modified(#BPFContentShareItemModified) Modifiedby(#BPFContentShareItemModifiedBy) Modifiedname(#BPFSessionEventValue3) Status(#Status)
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    #BPFContentShareItemName := #NewName
    
    Signal Event(OnItemRenameCompleted)
    
    Else
    
    Signal Event(OnItemRenameFailed)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnItemRenameFailed)
    
    Endroutine
    
    Endroutine
    
    Mthroutine Name(GetObject)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFContentSharingDataServer.GetItemObject) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Item(#BPFContentShareItemContentID) Object(#BPFContentItemObject) Status(#Status)
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    Signal Event(OnGetObjectCompleted)
    
    Else
    
    Signal Event(OnGetObjectFailed)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnGetObjectFailed)
    
    Endroutine
    
    Endroutine
    
    * Constructors
    Mthroutine Name(NewItem) Options(*CONSTRUCTOR)
    Define_Map For(*INPUT) Class(#PRIM_LCOL<#BPFContentShareItemClass>) Name(#Data) Pass(*BY_REFERENCE)
    
    * Map Data
    #BPFContentShareItemID := #Data<1>.Identifier
    #BPFContentShareItemName := #Data<1>.ItemName
    #BPFContentItemName := #Data<1>.ContentName
    #BPFContentShareItemIsFolder := #Data<1>.IsFolder
    #BPFContentShareItemParentID := #Data<1>.ParentID
    #BPFContentItemTokenFileName := #Data<1>.ContentToken
    #BPFContentItemAlphaDetail := #Data<1>.AlphaValue
    #BPFContentItemNumericDetail := #Data<1>.NumericValue
    #BPFContentShareItemContentID := #Data<1>.ContentIdentifier
    #BPFContentShareItemCreatedBy := #Data<1>.ItemCreatedByID
    #BPFContentShareItemCreated := #Data<1>.ItemCreated
    #BPFContentShareItemModifiedBy := #Data<1>.ItemModifiedByID
    #BPFContentShareItemModified := #Data<1>.ItemModified
    
    Endroutine
    
    
    * Private
    
    End_Com
