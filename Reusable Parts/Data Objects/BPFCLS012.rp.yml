Reusable Part:
  Name:               BPFUserProfileClass
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Portal User Profile Data Class"

  - JPN:
      Description:        "Portal User Profile Data Class"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_OBJT *LISTFIELDS #ListFields)
    
    * Group by For Field Mapping
    Def_List Name(#ListFields) Fields(#BPFProfileCompanyName #BPFProfileJobTitle #BPFProfileAddressLine1 #BPFProfileAddressLine2 #BPFProfileSuburbName #BPFProfilePostalCode #BPFProfileStateName #BPFProfileCountry #BPFCodeDescription #BPFProfileBirthDate #BPFProfilePhoneNumber) Type(*WORKING) Entrys(1)
    
    * Definitions
    
    * Properties
    
    * Fields
    Define_Pty Name(ProfileCompany) Get(*AUTO #BPFProfileCompanyName) Set(*AUTO #BPFProfileCompanyName)
    Define_Pty Name(JobTitle) Get(*AUTO #BPFProfileJobTitle) Set(*AUTO #BPFProfileJobTitle)
    Define_Pty Name(AddressLine1) Get(*AUTO #BPFProfileAddressLine1) Set(*AUTO #BPFProfileAddressLine1)
    Define_Pty Name(AddressLine2) Get(*AUTO #BPFProfileAddressLine2) Set(*AUTO #BPFProfileAddressLine2)
    Define_Pty Name(Suburb) Get(*AUTO #BPFProfileSuburbName) Set(*AUTO #BPFProfileSuburbName)
    Define_Pty Name(PostalCode) Get(*AUTO #BPFProfilePostalCode) Set(*AUTO #BPFProfilePostalCode)
    Define_Pty Name(State) Get(*AUTO #BPFProfileStateName) Set(*AUTO #BPFProfileStateName)
    Define_Pty Name(Country) Get(*AUTO #BPFProfileCountry) Set(*AUTO #BPFProfileCountry)
    Define_Pty Name(CountryDescription) Get(*AUTO #BPFCodeDescription)
    Define_Pty Name(BirthDate) Get(*AUTO #BPFProfileBirthDate) Set(*AUTO #BPFProfileBirthDate)
    Define_Pty Name(PhoneNumber) Get(*AUTO #BPFProfilePhoneNumber) Set(*AUTO #BPFProfilePhoneNumber)
    
    * Events
    
    * Create
    Define_Evt Name(OnSaveCompleted)
    Define_Evt Name(OnSaveFailed)
    
    * Routines
    
    * Methods
    
    Mthroutine Name(Save)
    
    * Define Request to Server Routine
    Define_Com Class(#BPFUserAccountDataServer.SetProfileData) Name(#Request)
    
    * Make Request
    #Request.ExecuteAsync Profiledata(#ListFields) Status(#Status)
    
    * Handle Request Events
    Evtroutine Handling(#Request.Completed)
    
    If (#Status = OK)
    
    
    Signal Event(OnSaveCompleted)
    
    Else
    
    Signal Event(OnSaveFailed)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#Request.Failed) Handled(#Handled) Reason(#Reason)
    
    #Handled := True
    Signal Event(OnSaveFailed)
    
    Endroutine
    
    Endroutine
    
    * Constructors
    Mthroutine Name(Create) Options(*CONSTRUCTOR)
    Define_Map For(*INPUT) Class(#Prim_LCOL<#BPFUserProfileClass>) Name(#Data) Pass(*BY_REFERENCE)
    
    * Map Values from the server
    #BPFProfileCompanyName := #Data<1>.ProfileCompany.trim
    #BPFProfileJobTitle := #Data<1>.JobTitle.trim
    #BPFProfileAddressLine1 := #Data<1>.AddressLine1.trim
    #BPFProfileAddressLine2 := #Data<1>.AddressLine2.trim
    #BPFProfileSuburbName := #Data<1>.Suburb.trim
    #BPFProfilePostalCode := #Data<1>.PostalCode.trim
    #BPFProfileStateName := #Data<1>.State.trim
    #BPFProfileCountry := #Data<1>.Country.trim
    #BPFCodeDescription := #Data<1>.CountryDescription.trim
    #BPFProfileBirthDate := #Data<1>.BirthDate
    #BPFProfilePhoneNumber := #Data<1>.PhoneNumber.trim
    
    Endroutine
    
    End_Com
