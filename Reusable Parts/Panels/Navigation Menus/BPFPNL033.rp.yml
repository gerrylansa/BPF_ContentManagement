Reusable Part:
  Name:               BPFAdminUserAccountsMenu
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Administration User Accounts Menu"

  - JPN:
      Description:        "Administration User Accounts Menu"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Height(1080) Tabposition(1) Layoutmanager(#PanelLayout) Width(280)
    
    * Definitions
    
    * Global Objects
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFAdminUserAccountMenuStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#FontWeight500Style) Fontweight(Weight500)
    
    * Panel Layout Manager
    Define_Com Class(#PRIM_TBLO) Name(#PanelLayout) Sizing(ContentWidth)
    
    Define_Com Class(#PRIM_TBLO.Column) Name(#PanelLayoutColumn1) Displayposition(1) Parent(#PanelLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#PanelLayoutRow1) Displayposition(1) Parent(#PanelLayout)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PanelLayoutItem1) Alignment(TopLeft) Column(#PanelLayoutColumn1) Manage(#Appdrawer) Parent(#PanelLayout) Row(#PanelLayoutRow1) Sizing(FitToHeight)
    
    * App Drawer Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#PanelLayoutItem2) Manage(#MenuHeading) Parent(#PanelLayout) Row(#PanelLayoutRow1) Sizing(None) Column(#PanelLayoutColumn1) Alignment(TopLeft) Flow(Down)
    
    Define_Com Class(#PRIM_TBLO) Name(#AppdrawerLayout)
    
    * Columns and Rows
    Define_Com Class(#PRIM_TBLO.Column) Name(#AppdrawerLayoutColumn1) Displayposition(1) Parent(#AppdrawerLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#AppdrawerLayoutRow1) Displayposition(1) Parent(#AppdrawerLayout) Units(Content) Height(99)
    Define_Com Class(#PRIM_TBLO.Row) Name(#AppdrawerLayoutRow2) Displayposition(2) Parent(#AppdrawerLayout) Units(Content) Height(31) Minheight(80)
    Define_Com Class(#PRIM_TBLO.Row) Name(#AppdrawerLayoutRow4) Displayposition(3) Parent(#AppdrawerLayout)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#AppdrawerLayoutItem1) Column(#AppdrawerLayoutColumn1) Manage(#AppDrawerMenu) Parent(#AppdrawerLayout) Row(#AppdrawerLayoutRow4) Margintop(8) Sizing(FitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#AppdrawerLayoutItem4) Manage(#AppDrawerSlideHeading) Parent(#AppdrawerLayout) Row(#AppdrawerLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#AppdrawerLayoutColumn1) Alignment(TopLeft)
    Define_Com Class(#PRIM_TBLO.Item) Name(#AppdrawerLayoutItem2) Manage(#MenuHeading) Parent(#AppdrawerLayout) Row(#AppdrawerLayoutRow2) Sizing(ContentHeightFitToWidth) Column(#AppdrawerLayoutColumn1) Alignment(CenterLeft) Marginleft(16) Marginright(16)
    
    Define_Com Class(#PRIM_TBLO) Name(#AppDrawerMenuLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#AppDrawerMenuLayoutRow1) Displayposition(1) Parent(#AppDrawerMenuLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#AppDrawerMenuLayoutColumn1) Displayposition(1) Parent(#AppDrawerMenuLayout)
    Define_Com Class(#PRIM_TBLO.Item) Name(#AppDrawerMenuLayoutItem1) Alignment(BottomCenter) Column(#AppDrawerMenuLayoutColumn1) Manage(#AboutLink) Parent(#AppDrawerMenuLayout) Row(#AppDrawerMenuLayoutRow1) Sizing(ContentWidthAndHeight) Flow(CenterHorizontal)
    Define_Com Class(#PRIM_TBLO.Item) Name(#AppDrawerMenuLayoutItem2) Alignment(BottomCenter) Column(#AppDrawerMenuLayoutColumn1) Manage(#TermsLink) Parent(#AppDrawerMenuLayout) Row(#AppDrawerMenuLayoutRow1) Sizing(ContentWidthAndHeight) Flow(CenterHorizontal)
    Define_Com Class(#PRIM_TBLO.Item) Name(#AppDrawerMenuLayoutItem3) Alignment(BottomCenter) Column(#AppDrawerMenuLayoutColumn1) Manage(#LinkSpacer) Parent(#AppDrawerMenuLayout) Row(#AppDrawerMenuLayoutRow1) Sizing(None) Flow(CenterHorizontal) Marginleft(4) Marginright(4)
    
    * App Drawer
    Define_Com Class(#PRIM_MD.AppDrawer) Name(#Appdrawer) Displayposition(1) Layoutmanager(#AppdrawerLayout) Parent(#COM_OWNER) Tabposition(1) Width(280) Height(1080) Themedrawstyle('ForegroundSecondary+Heading3')
    
    * App Drawer Controls
    Define_Com Class(#PRIM_MD.Label) Name(#AppDrawerSlideHeading) Caption('myPortal') Displayposition(3) Left(0) Parent(#Appdrawer) Tabposition(3) Themedrawstyle('MediumTitle+Heading2') Width(280) Height(99) Captionmarginleft(16) Captionmarginbottom(8) Captionmargintop(0) Captionmarginright(16) Icon('cloud') Iconalignment(AboveCaption) Iconmarginleft(16) Iconheight(60) Iconmargintop(8) Top(0) Visible(False)
    
    * Menu Heading
    Define_Com Class(#PRIM_MD.Label) Name(#MenuHeading) Displayposition(1) Left(16) Parent(#Appdrawer) Tabposition(1) Themedrawstyle('Heading1') Top(124) Width(248) Icon('supervisor_account') Captionmarginleft(24) Height(31) Wordwrap(True) Caption('User Accounts') Style(#NavigationLinkStyle)
    
    * Menu
    Define_Com Class(#PRIM_MD.Menu) Name(#AppDrawerMenu) Displayposition(2) Height(893) Parent(#Appdrawer) Tabposition(2) Width(279) Top(187) Style(#FontWeight500Style) Layoutmanager(#AppDrawerMenuLayout) Left(1)
    
    * Menu Items : Note that the componenttag contains a JSON string to support multiple values.
    Define_Com Class(#PRIM_MD.MenuItem) Name(#DetailsMenuItem) Caption('Details') Displayposition(1) Height(50) Icon('account_box') Left(0) Parent(#AppDrawerMenu) Tabposition(1) Top(0) Width(279) Themedrawstyle('TopDivider') Componenttag('{"event":"adminuseraccountview","keyParameter1":"value"}') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.MenuItem) Name(#ActionsMenuItem) Caption('Account Actions') Displayposition(2) Height(50) Icon('build') Left(0) Parent(#AppDrawerMenu) Tabposition(2) Top(50) Width(279) Componenttag('{"event":"adminuserselectaccountaction","keyParameter1":"value"}') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.MenuItem) Name(#ContentShareMenuItem) Caption('Content Share') Displayposition(4) Height(50) Icon('folder_shared') Left(0) Parent(#AppDrawerMenu) Tabposition(8) Top(150) Width(279) Componenttag('{"event":"adminusersetcontentshare","keyParameter1":"value"}') Style(#NavigationLinkStyle) Visible(False)
    Define_Com Class(#PRIM_MD.MenuItem) Name(#AdminRightsMenuItem) Caption('Administration Rights') Displayposition(5) Height(50) Icon('supervisor_account') Left(0) Parent(#AppDrawerMenu) Tabposition(5) Top(200) Width(279) Componenttag('{"event":"adminaccountverification","keyParameter1":"value","keyValue1":"adminusersetadminrightsview","keyParameter2":"id"}') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.MenuItem) Name(#ResetMenuItem) Caption('Reset Security') Displayposition(6) Height(50) Icon('vpn_key') Left(0) Parent(#AppDrawerMenu) Tabposition(4) Top(250) Width(279) Componenttag('{"event":"adminaccountverification","keyParameter1":"value","keyValue1":"adminusersecurityreset","keyParameter2":"id"}') Style(#NavigationLinkStyle)
    
    Define_Com Class(#PRIM_MD.FlatButton) Name(#AboutLink) Caption('About') Displayposition(7) Left(81) Parent(#AppDrawerMenu) Tabposition(6) Top(862) Height(31) Themedrawstyle('Subheading') Captionmarginbottom(0) Captionmarginleft(0) Captionmarginright(0) Width(48) Captionmargintop(0) Style(#NavigationLinkStyle) Paddingbottom(8) Paddingleft(8) Paddingright(8) Paddingtop(8) Captionalignment(CenterRight)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#TermsLink) Caption('Terms') Displayposition(9) Left(147) Parent(#AppDrawerMenu) Tabposition(3) Top(862) Height(31) Themedrawstyle('Subheading') Captionmarginbottom(0) Captionmarginleft(0) Captionmarginright(0) Width(50) Captionmargintop(0) Style(#NavigationLinkStyle) Captionalignment(CenterLeft) Paddingbottom(8) Paddingleft(8) Paddingright(8) Paddingtop(8)
    Define_Com Class(#PRIM_MD.Label) Name(#LinkSpacer) Displayposition(8) Left(133) Parent(#AppDrawerMenu) Tabposition(7) Top(862) Height(31) Themedrawstyle('Subheading') Captionmarginbottom(8) Captionmarginleft(0) Captionmarginright(0) Width(10) Captionmargintop(8) Captionalignment(Center) Caption('-')
    Define_Com Class(#PRIM_MD.MenuItem) Name(#SessionEventsMenuItem) Caption('Session Events') Displayposition(3) Height(50) Icon('schedule') Left(0) Parent(#AppDrawerMenu) Tabposition(9) Top(100) Width(279) Componenttag('{"event":"adminusersessioneventsview","keyParameter1":"value"}') Style(#NavigationLinkStyle)
    
    * Menu Items
    Define_Pty Name(MenuItems) Get(*REFERENCE #NavigationButtons)
    Define_Com Class(#PRIM_KCOL<#PRIM_MD.MenuItem #BPFNavigationEventName>) Name(#NavigationButtons) Style(Collection)
    
    * Extrenal Control References
    Define_Com Class(#Prim_MD.AppBar) Name(#TheAppBar) Reference(*DYNAMIC)
    Define_Com Class(#BPFPortalNavigationDrawer) Name(#TheNavDrawer) Reference(*DYNAMIC)
    
    * Reference for current key
    Define_Com Class(#PRIM_ALPH) Name(#ContextKey)
    
    * Routines
    
    * Events
    
    * Self
    Evtroutine Handling(#Com_owner.CreateInstance)
    
    * Grab references for current context key
    #ContextKey := #SYS_WEB.URLParameters<VALUE>.Value
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    #AppDrawerSlideHeading.Icon := #BPFPortal.AppIcon
    #AppDrawerSlideHeading.Caption := #BPFPortal.AppName
    
    * Handle coming alive
    #COM_SELF.Initialise
    
    Endroutine
    
    * Webpage
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    #Com_self.HandleDeviceChanged
    
    Endroutine
    
    * Nav Drawer
    Evtroutine Handling(#TheNavDrawer.OnMenuMessage) Messagetext(#Message)
    
    #ContentShareMenuItem.Visible := (#Message.UpperCase = SHOWSHAREOPTION)
    
    Endroutine
    
    * Appbar
    Evtroutine Handling(#TheAppBar.IconClick)
    
    #Appdrawer.ToggleDrawer
    
    Endroutine
    
    Evtroutine Handling(#AboutLink.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #Navigate.To Eventname(PORTALABOUT)
    
    Endroutine
    
    Evtroutine Handling(#TermsLink.Click)
    
    #BPFPortal.ViewsPanel.SetWaitingState( ON )
    #Navigate.To Eventname(PORTALTERMSOFSERVICE)
    
    Endroutine
    
    Evtroutine Handling(#Navigate.OnNavigationEvent) Group(#GroupName) Name(#Event)
    
    If ((#SYS_WEB.Device <> Desktop) *AndIf (#Appdrawer.IsShowing))
    
    #Appdrawer.CloseDrawer
    
    Endif
    
    #COM_SELF.HandleNavigationEvent( #GroupName #Event )
    
    Endroutine
    
    Evtroutine Handling(#MenuHeading.Click)
    
    #Navigate.BackToTarget Target('adminuseraccountslistview') Targetmissingusehome(True)
    
    Endroutine
    
    * Controls
    
    Evtroutine Handling(#NavigationButtons<>.Click) Com_Sender(#Sender) Handled(#Handled)
    
    Define_Com Class(#PRIM_WEB.Json) Name(#EventWithKey)
    
    * Stop default Action
    #Handled := True
    
    #EventWithKey := #Sender.ComponentTag
    
    * Okay, the home button is the root navigation item so if you click this then we
    * are always going back to the root. The other items for the navigation at the top
    * level which is all we have at the moment are 1 step up from the root. Thus what we
    * want to do is always go back to the root, then step forward 1.
    
    * This is true no matter where we are in the app.
    
    * So, did we click the home or something else?
    If (#EventWithKey.RootItem<'event'>.AsString = 'adminuseraccountview')
    
    * We did, so let go back to root
    #Navigate.BackToTarget Target(#EventWithKey.RootItem<'event'>.AsString) Newquerystring((#EventWithKey.RootItem<'keyParameter1'>.AsString + '=' + #ContextKey)) Targetmissingusehome(True)
    
    Else
    
    * We did not, so lets get the navigation manager to navigate back to the root and then
    * take 1 step forward
    * #Navigate.BackToTarget( 'adminuseraccountview' #Sender.ComponentTag False )
    If (#EventWithKey.RootItem<'keyParameter2'> *Is *NULL)
     
    #Navigate.BackToTarget( 'adminuseraccountview' #EventWithKey.RootItem<'event'>.AsString False true (#EventWithKey.RootItem<'keyParameter1'>.AsString + '=' + #ContextKey) )
    
    Else
    
    #Navigate.BackToTarget( 'adminuseraccountview' #EventWithKey.RootItem<'event'>.AsString False true (#EventWithKey.RootItem<'keyParameter1'>.AsString + '=' + #EventWithKey.RootItem<'keyValue1'>.AsString + '&' + #EventWithKey.RootItem<'keyParameter2'>.AsString + '=' + #ContextKey) )
    
    Endif
    
    Endif
    
    Endroutine
    
    * Set up of Object
    Mthroutine Name(Initialise)
    
    * Extrenal References
    #TheNavDrawer <= #BPFPortal.NavDrawer
    #TheAppBar <= #BPFPortal.AppBar.AppBar
    
    * Collect Menu Items
    #COM_SELF.CollectAppDrawerMenuItems
    
    * Set Strings
    #MENUHEADING.CAPTION := #STRINGS.Text<MENUHEADING.CAPTION>
    #ABOUTLINK.CAPTION := #STRINGS.Text<ABOUTLINK.CAPTION>
    #TERMSLINK.CAPTION := #STRINGS.Text<TERMSLINK.CAPTION>
    
    * Set Inital Display
    #Com_self.HandleDeviceChanged
    
    Endroutine
    
    Mthroutine Name(HandleNavigationEvent) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Eventgroup)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Event)
    
    * If we have event from the view group then we need to take action
    If ((#Eventgroup = VIEWS))
    
    * If we have a matching menu item, then attach current view
    If (#NavigationButtons<#Navigate.CurrentEvent.EventName> *IsNot *null)
    
    * #NavigationButtons<#Navigate.CurrentEvent.EventName>.View <= #Navigate.CurrentEvent.RelatedView
    
    #NavigationButtons<>.View <= *Null
    #NavigationButtons<#Navigate.CurrentEvent.EventName>.View <= #COM_OWNER
    
    Else
    
    If (#SYS_WEB.URLParameters<VALUE> *IsNot *Null)
     
    If (#NavigationButtons<#SYS_WEB.URLParameters<VALUE>.value> *IsNot *null)
    
    * #NavigationButtons<#SYS_WEB.URLParameters<VALUE>.value>.View <= #Navigate.CurrentEvent.RelatedView
    
    #NavigationButtons<>.View <= *Null
    #NavigationButtons<#SYS_WEB.URLParameters<VALUE>.value>.View <= #COM_OWNER
    
    Endif
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(CollectAppDrawerMenuItems)
    
    Define_Com Class(#PRIM_WEB.Json) Name(#EventWithKey)
    Define_Com Class(#PRIM_BOLN) Name(#SetDefault)
    
    * Foe the App Drawer menu collect the items
    For Each(#Item) In(#AppDrawerMenu.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.MENUItem)
    
    * Set initial selected menu item
    If (#SetDefault.IsFalse)
    
    * #Item.View <= #Navigate.CurrentEvent.RelatedView
    #Item.View <= #COM_OWNER
    
    #SetDefault := true
    
    Endif
    
    #Item.Caption := #Strings.Text<(#Item.Name.UpperCase + '.CAPTION')>
    #EventWithKey := #Item.ComponentTag
    
    * Set Item Key
    If (#EventWithKey.RootItem<'keyValue1'> *Is *NULL)
    
    #NavigationButtons<#EventWithKey.RootItem<'event'>.AsString> <= #Item
    
    Else
    
    #NavigationButtons<#EventWithKey.RootItem<'keyValue1'>.AsString> <= #Item
    
    Endif
    
    Endfor
    
    Endroutine
    
    * Handle Responsive Layout Changes
    Mthroutine Name(HandleDeviceChanged)
    
    Case Of_Field(#SYS_WEB.Device)
    
    When Value_Is(= Mobile)
    
    #COM_SELF.SetForMobile
    
    When Value_Is(= Tablet)
    
    #COM_SELF.SetForTablet
    
    When Value_Is(= Desktop)
    
    #COM_SELF.SetForDesktop
    
    Endcase
    
    Endroutine
    
    * Mobile Layout
    Mthroutine Name(SetForMobile)
    
    * Set App Drawer
    #Appdrawer.DrawerStyle := SlideInLeft
    #com_self.Width := #SYS_WEB.WebPage.Width * 0.80
    #AppDrawerSlideHeading.Visible := True
    
    Endroutine
    
    * Tablet Layout
    Mthroutine Name(SetForTablet)
    
    * Set App Drawer
    #Appdrawer.DrawerStyle := SlideInLeft
    #com_self.Width := 300
    #AppdrawerSlideHeading.Visible := True
    
    Endroutine
    
    * Desktop Layout
    Mthroutine Name(SetForDesktop)
    
    * Set App Drawer
    #Appdrawer.DrawerStyle := Permanent
    #com_self.Width := 280
    #AppDrawerSlideHeading.Visible := False
    
    Endroutine
    
    End_Com
