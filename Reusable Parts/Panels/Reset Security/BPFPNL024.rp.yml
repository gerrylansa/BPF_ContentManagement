Reusable Part:
  Name:               BPFResetSecurityDetailsPanel
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Portal Reset SecurityDetails Panel"

  - JPN:
      Description:        "Portal Reset SecurityDetails Panel"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_View) Displayposition(1) Height(426) Tabposition(1) Width(666) Layoutmanager(#Layout)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFResetSecurityDetailsPanelStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#Prim_vs.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Panel Layouts
    
    * Base Layout
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(3) Parent(#Layout) Units(Content) Width(252)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(2) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#Layout) Height(48) Units(Pixels)
    
    * Detail Panel Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#DialogLogo) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem2) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#MainInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem3) Alignment(TopLeft) Column(#LayoutColumn1) Flow(Down) Manage(#SecondaryInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Marginbottom(24) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem4) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#SecurityQuestionSelection) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToWidth) Flow(Down) Marginright(8) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem6) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#SecurityAnswerEdit) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(8) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem10) Alignment(CenterRight) Column(#LayoutColumn3) Manage(#DetailsPanelNextButton) Parent(#Layout) Row(#LayoutRow2) Sizing(None)
    
    * Mobile Layout
    Define_Com Class(#PRIM_TBLO) Name(#MobileLayout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MobileLayoutColumn1) Displayposition(1) Parent(#MobileLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MobileLayoutRow1) Displayposition(1) Parent(#MobileLayout)
    
    * Details Panel Mobile Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem1) Manage(#MainInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#MobileLayoutColumn1) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem2) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecondaryInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem3) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecurityQuestionSelection) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem5) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecurityAnswerEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem9) Alignment(BottomRight) Column(#MobileLayoutColumn1) Manage(#DetailsPanelNextButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    
    Define_Com Class(#PRIM_MD.Label) Name(#DialogLogo) Caption('myPortal. It''s your portal protecting your account') Displayposition(1) Left(414) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading3+ForegroundTheme') Top(0) Height(378) Width(252) Captionalignment(Center) Wordwrap(True) Iconalignment(AboveCaption) Icon('lock') Iconheight(118) Iconthemedrawstyle('ForegroundMediumPrimary') Captionmarginleft(14) Captionmarginright(14)
    Define_Com Class(#PRIM_MD.Label) Name(#MainInstruction) Caption('Secure your myPortal account') Displayposition(2) Left(0) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('Heading2') Top(0) Height(27) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Label) Name(#SecondaryInstruction) Caption('by setting a new security question and answer ') Displayposition(3) Left(0) Parent(#COM_OWNER) Tabposition(3) Top(27) Height(21) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#SecurityQuestionSelection) Caption('Select a question') Displayposition(4) Left(0) Parent(#COM_OWNER) Tabposition(4) Top(72) Width(406)
    Define_Com Class(#PRIM_MD.Edit) Name(#SecurityAnswerEdit) Caption('Answer') Displayposition(5) Left(0) Parent(#COM_OWNER) Tabposition(5) Top(155) Width(414) Helpertext('be sure to take note of your answer') Componenttag('This will be your username')
    Define_Com Class(#PRIM_MD.RaisedButton) Name(#DetailsPanelNextButton) Caption('Next') Displayposition(6) Left(334) Parent(#COM_OWNER) Tabposition(6) Themedrawstyle('MediumTitle') Top(384) Width(80) Style(#NavigationLinkStyle)
    
    * Parent
    Define_Com Class(#BPFResetAccountSecurityDialog) Name(#Dialog) Reference(*DYNAMIC)
    
    * Reference to Data Object
    Define_Com Class(#BPFUserAccountClass) Name(#ResetAccount) Reference(*DYNAMIC)
    
    * Rotuines
    
    * Events
    
    * Creation
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get reference to parent
    #Dialog <= #BPFPortal.SecurityResetDialog
    #ResetAccount <= *New #BPFUserAccountClass
    
    #COM_SELF.SetStrings
    
    #COM_SELF.HandleDeviceChanged
    
    * Setup Panel
    #COM_SELF.HandleDisplay
    
    Endroutine
    
    * Webpage
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    #COM_SELF.HandleDeviceChanged
    
    Endroutine
    
    * Session
    Evtroutine Handling(#Session.OnGetSecurityQuestionCompleted) Questions(#Questions)
    
    #COM_SELF.BuildQuestionList( #Questions )
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnGetSecurityQuestionFailed)
    
    #COM_SELF.HandleListFailure
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#ResetAccount.OnSetSecurityCompleted)
    
    #Dialog.SetWaitingState( OFF )
    #Navigate.To Eventname(RESETACCOUNTSECURITYPASSWORD)
    
    Endroutine
    
    Evtroutine Handling(#ResetAccount.OnSetSecurityFailed) Reason(#Reason)
    
    #Dialog.SetWaitingState( OFF )
    #Navigate.To( 'resetaccountsecurityfailed' ADD true 'details' )
    
    Endroutine
    
    * Actions
    
    Evtroutine Handling(#DetailsPanelNextButton.Click #SecurityAnswerEdit.Enter)
    
    If (#COM_SELF.CheckDataIsValid)
    
    #Dialog.SetWaitingState( ON )
    #ResetAccount.SetSecurityDetails( #SecurityQuestionSelection.value #SecurityAnswerEdit.value )
    
    Endif
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #MAININSTRUCTION.CAPTION := #STRINGS.Text<MAININSTRUCTION.CAPTION>
    #SECONDARYINSTRUCTION.CAPTION := #STRINGS.Text<SECONDARYINSTRUCTION.CAPTION>
    #SECURITYQUESTIONSELECTION.CAPTION := #STRINGS.Text<SECURITYQUESTIONSELECTION.CAPTION>
    #SECURITYANSWEREDIT.CAPTION := #STRINGS.Text<SECURITYANSWEREDIT.CAPTION>
    #SECURITYANSWEREDIT.HELPERTEXT := #STRINGS.Text<SECURITYANSWEREDIT.HELPERTEXT>
    #DIALOGLOGO.CAPTION := #STRINGS.Text<DIALOGLOGO.CAPTION>
    #DETAILSPANELNEXTBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELNEXTBUTTON.CAPTION>
    
    Endroutine
    
    * Handle Display of Panel
    Mthroutine Name(HandleDisplay)
    
    #Session.GetSecurityQuestions
    
    Endroutine
    
    * Build Question List
    Mthroutine Name(BuildQuestionList)
    
    * Map In Questions
    Define_Map For(*INPUT) Class(#PRIM_LCOL<#BPFAlphaValuesItemClass>) Name(#QuestionsList) Pass(*BY_REFERENCE)
    
    * Ensure List is Clear
    #SecurityQuestionSelection.Items.RemoveAll
    
    * Build List
    For Each(#Question) In(#QuestionsList)
    
    #SecurityQuestionSelection.Items.Add Value(#Question.Value) Caption(#Question.Value)
    
    Endfor
    
    Endroutine
    
    Mthroutine Name(HandleListFailure)
    
    #SecurityQuestionSelection.ShowError( #STRINGS.Text<SECURITYQUESTIONSELECTION.HELPERTEXT.EMPTY> )
    
    Endroutine
    
    * Validation of Data
    Mthroutine Name(CheckDataIsValid)
    Define_Map For(*RESULT) Class(#PRIM_BOLN) Name(#Result)
    
    * Assume Success
    #Result := True
    #Com_self.ClearAllErrors
    
    #Result := ((#SecurityQuestionSelection.value <> *blank) *And (#SecurityAnswerEdit.Value <> *Blank))
    
    Endroutine
    
    Mthroutine Name(ClearAllErrors) Access(*PRIVATE)
    
    * Check if we have any errors
    For Each(#Control) In(#COM_SELF.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.Edit)
    
    If (#Control.HasError)
    
    #Control.ClearError
    
    Endif
    
    Endfor
    
    Endroutine
    
    * Responsive Design
    
    Mthroutine Name(HandleDeviceChanged) Access(*PRIVATE)
    
    Case Of_Field(#SYS_WEB.Device)
    
    When Value_Is(= Mobile)
    
    #COM_SELF.SetForMobile
    
    When Value_Is(= Tablet)
    
    #COM_SELF.SetForTablet
    
    Otherwise
    
    #COM_SELF.SetForDesktop
    
    Endcase
    
    Endroutine
    
    Mthroutine Name(SetForMobile) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #MobileLayout)
    
    #COM_SELF.LayoutManager <= #MobileLayout
    
    Endif
    Endroutine
    
    Mthroutine Name(SetForTablet) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetForDesktop) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := True
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    End_Com
