Reusable Part:
  Name:               BPFNewAccountDetailsPanel
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Portal New Account Details"

  - JPN:
      Description:        "Portal New Account Details"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_View) Displayposition(1) Height(426) Tabposition(1) Width(666) Layoutmanager(#Layout)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFNewAccountDetailsPanelStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#Prim_vs.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Panel Layouts
    
    * Base Layout
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(3) Parent(#Layout) Units(Content) Width(252)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(2) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#Layout) Height(48) Units(Content) Minheight(48)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow3) Displayposition(3) Parent(#Layout) Height(48) Units(Pixels)
    
    * Detail Panel Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#DialogLogo) Parent(#Layout) Row(#LayoutRow2) Sizing(FitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem2) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#MainInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem3) Alignment(TopLeft) Column(#LayoutColumn1) Flow(Down) Manage(#SecondaryInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem4) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#FirstNameEdit) Parent(#Layout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Marginright(8) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem5) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#LastNameEdit) Parent(#Layout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(24) Marginleft(8)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem6) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#EmailAddressEdit) Parent(#Layout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(8) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem7) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#UserPasswordEdit) Parent(#Layout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(16) Marginright(9)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem8) Alignment(TopLeft) Column(#LayoutColumn3) Manage(#ConfirmPasswordEdit) Parent(#Layout) Row(#LayoutRow2) Sizing(FitToWidth) Flow(Down) Margintop(99) Marginleft(9)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem9) Alignment(CenterLeft) Column(#LayoutColumn1) Manage(#DetailsPanelActionButton) Parent(#Layout) Row(#LayoutRow3) Sizing(ContentWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem10) Alignment(CenterRight) Column(#LayoutColumn3) Manage(#DetailsPanelNextButton) Parent(#Layout) Row(#LayoutRow3) Sizing(None)
    
    * Mobile Layout
    Define_Com Class(#PRIM_TBLO) Name(#MobileLayout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MobileLayoutColumn1) Displayposition(1) Parent(#MobileLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MobileLayoutRow1) Displayposition(1) Parent(#MobileLayout)
    
    * Details Panel Mobile Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem1) Manage(#MainInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#MobileLayoutColumn1) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem2) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecondaryInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem3) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#FirstNameEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem4) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#LastNameEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem5) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#EmailAddressEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem6) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#UserPasswordEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem7) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#ConfirmPasswordEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem8) Alignment(BottomLeft) Column(#MobileLayoutColumn1) Manage(#DetailsPanelActionButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem9) Alignment(BottomRight) Column(#MobileLayoutColumn1) Manage(#DetailsPanelNextButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    
    Define_Com Class(#PRIM_MD.Label) Name(#DialogLogo) Caption('myPortal. It''s your portal anyway you want it') Displayposition(1) Left(414) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading3+ForegroundTheme') Top(48) Height(330) Width(252) Captionalignment(Center) Wordwrap(True) Iconalignment(AboveCaption) Icon('cloud') Iconheight(118) Iconthemedrawstyle('ForegroundMediumPrimary') Captionmarginleft(14) Captionmarginright(14)
    Define_Com Class(#PRIM_MD.Label) Name(#MainInstruction) Caption('Create your myPortal account') Displayposition(2) Left(0) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('Heading2') Top(0) Height(27) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Label) Name(#SecondaryInstruction) Caption('to continue enter the details below') Displayposition(3) Left(0) Parent(#COM_OWNER) Tabposition(3) Top(27) Height(21) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Edit) Name(#FirstNameEdit) Caption('First name') Displayposition(4) Left(0) Parent(#COM_OWNER) Tabposition(4) Top(72) Width(199)
    Define_Com Class(#PRIM_MD.Edit) Name(#LastNameEdit) Caption('Last name') Displayposition(5) Left(215) Parent(#COM_OWNER) Tabposition(5) Top(72) Width(199)
    Define_Com Class(#PRIM_MD.Edit) Name(#EmailAddressEdit) Caption('Email Address') Displayposition(6) Left(0) Parent(#COM_OWNER) Tabposition(6) Top(155) Width(414) Helpertext('This will be your username') Componenttag('This will be your username')
    Define_Com Class(#PRIM_MD.Edit) Name(#UserPasswordEdit) Caption('Password') Displayposition(7) Left(0) Parent(#COM_OWNER) Tabposition(7) Top(246) Width(198) Height(83) Passwordchar('*')
    Define_Com Class(#PRIM_MD.Edit) Name(#ConfirmPasswordEdit) Caption('Confirm') Displayposition(8) Left(216) Parent(#COM_OWNER) Tabposition(8) Top(246) Width(198) Height(83) Passwordchar('*')
    Define_Com Class(#PRIM_MD.FlatButton) Name(#DetailsPanelActionButton) Caption('Sign in instead') Displayposition(9) Left(0) Parent(#COM_OWNER) Tabposition(9) Themedrawstyle('ForegroundMediumPrimary') Top(384) Style(#NavigationLinkStyle) Captionmarginleft(4) Captionmarginright(4)
    Define_Com Class(#PRIM_MD.RaisedButton) Name(#DetailsPanelNextButton) Caption('Next') Displayposition(10) Left(334) Parent(#COM_OWNER) Tabposition(10) Themedrawstyle('MediumTitle') Top(384) Width(80) Style(#NavigationLinkStyle)
    
    * Parent
    Define_Com Class(#BPFCreateAccountDialog) Name(#Dialog) Reference(*DYNAMIC)
    
    * Reference to Data Object
    Define_Com Class(#BPFUserAccountClass) Name(#NewAccount) Reference(*DYNAMIC)
    
    
    * Events
    Define_Evt Name(ToggleCoverPanel)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#ForShow) Mandatory(False)
    
    * Non Visual Objects
    Define_Com Class(#PRIM_BOLN) Name(#AccountInUse)
    Define_Com Class(#PRIM_TIMR) Name(#AccountCheckDelay) Interval(250) Startup(Manual)
    
    * Rotuines
    
    * Events
    
    * Creation
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get reference to parent and data
    #Dialog <= #BPFPortal.CreateDialog
    #NewAccount <= #Dialog.NewAccount
    
    #COM_SELF.SetStrings
    
    #COM_SELF.HandleDeviceChanged
    
    #COM_SELF.HandleDisplay
    
    Endroutine
    
    * Webpage
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    #COM_SELF.HandleDeviceChanged
    
    Endroutine
    
    * Session
    Evtroutine Handling(#Session.OnCheckAccountCompleted) Accountexists(#Exists)
    
    #COM_SELF.HandleAccountExists( #Exists )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnCheckAccountFailed)
    
    #EmailAddressEdit.ShowError( #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT.FAILED> )
    
    Endroutine
    
    Evtroutine Handling(#NewAccount.OnDetailsStoredCompleted)
    
    #Dialog.SetWaitingState( OFF )
    #Navigate.To Eventname(CREATEUSERSECURITY)
    
    Endroutine
    
    Evtroutine Handling(#NewAccount.OnDetailsStoredFailed) Reason(#Reason)
    
    #Dialog.SetWaitingState( OFF )
    
    Case Of_Field(#Reason)
    
    When Value_Is(= EXISTS)
    
    #COM_SELF.HandleAccountExists( True )
    
    Otherwise
    
    #Navigate.To Eventname(NEWACCOUNTFAILED) Value(#Reason)
    
    Endcase
    
    Endroutine
    
    * Fields
    Evtroutine Handling(#EmailAddressEdit.Changed)
    
    If (#EmailAddressEdit.Value.IsValidEmail)
    
    #AccountCheckDelay.Stop
    #AccountCheckDelay.Start
    
    Else
    
    #EmailAddressEdit.HelperColor := ''
    #EmailAddressEdit.HelperText := #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT>
    
    Endif
    
    Endroutine
    
    * Timers
    Evtroutine Handling(#AccountCheckDelay.Tick)
    
    #AccountCheckDelay.Stop
    #Session.CheckForAccount( #EmailAddressEdit )
    
    Endroutine
    
    * Actions
    Evtroutine Handling(#DetailsPanelActionButton.Click)
    
    If (#BPFPortal.LoginDialog *Is *NULL)
    
    #SYS_WEB.History.GoBack
    
    Else
    
    #Navigate.To Eventname(*null) Type(REPLACE) Signalreplaceevent(True)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#DetailsPanelNextButton.Click #ConfirmPasswordEdit.Enter)
    
    If (#COM_SELF.CheckDataIsValid)
    
    #Dialog.SetWaitingState( ON )
    #NewAccount.StoreDetails( #UserPasswordEdit )
    
    Endif
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #MAININSTRUCTION.CAPTION := #STRINGS.Text<MAININSTRUCTION.CAPTION>
    #SECONDARYINSTRUCTION.CAPTION := #STRINGS.Text<SECONDARYINSTRUCTION.CAPTION>
    #DIALOGLOGO.CAPTION := #STRINGS.Text<DIALOGLOGO.CAPTION>
    #FIRSTNAMEEDIT.CAPTION := #STRINGS.Text<FIRSTNAMEEDIT.CAPTION>
    #LASTNAMEEDIT.CAPTION := #STRINGS.Text<LASTNAMEEDIT.CAPTION>
    #EMAILADDRESSEDIT.CAPTION := #STRINGS.Text<EMAILADDRESSEDIT.CAPTION>
    #EMAILADDRESSEDIT.HELPERTEXT := #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT>
    #USERPASSWORDEDIT.CAPTION := #STRINGS.Text<USERPASSWORDEDIT.CAPTION>
    #CONFIRMPASSWORDEDIT.CAPTION := #STRINGS.Text<CONFIRMPASSWORDEDIT.CAPTION>
    #DETAILSPANELACTIONBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELACTIONBUTTON.CAPTION>
    #DETAILSPANELNEXTBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELNEXTBUTTON.CAPTION>
    
    Endroutine
    
    * Handle Panel Display
    Mthroutine Name(HandleDisplay)
    
    * Initialise Display Fields
    #FirstNameEdit := #NewAccount.FirstName
    #LastNameEdit := #NewAccount.LastName
    #EmailAddressEdit := #NewAccount.EmailAdress
    
    * Do NOT populate password fields
    
    Endroutine
    
    Mthroutine Name(HandleAccountExists)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#Exists)
    
    #AccountInUse := #Exists
    
    If (#AccountInUse)
    
    #EmailAddressEdit.ShowError( #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT.EXISTS> )
    
    Else
    
    #EmailAddressEdit.ClearError
    #EmailAddressEdit.HelperColor := 'ThemeSuccessMedium'
    #EmailAddressEdit.HelperText := #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT.OK>
    
    Endif
    
    Endroutine
    
    * Validation of Data
    Mthroutine Name(CheckDataIsValid)
    Define_Map For(*RESULT) Class(#PRIM_BOLN) Name(#Result)
    
    * Assume Success
    #Result := True
    #Com_self.ClearAllErrors
    
    * Check for First name
    If (#FirstNameEdit.value.IsNull)
    
    #FirstNameEdit.ShowError( #STRINGS.Text<FIRSTNAMEEDIT.HELPERTEXT.BLANK> )
    
    Else
    
    #NewAccount.FirstName := #FirstNameEdit
    
    Endif
    
    * Check for Last Name
    If (#LastNameEdit.value.IsNull)
    
    #LastNameEdit.ShowError( #STRINGS.Text<LASTNAMEEDIT.HELPERTEXT.BLANK> )
    
    Else
    
    #NewAccount.LastName := #LastNameEdit
    
    Endif
    
    * Check email for blank in use
    If (#EmailAddressEdit.value.IsNull)
    
    #EmailAddressEdit.ShowError( #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT.BLANK> )
    
    Else
    
    If (#EmailAddressEdit.value.IsValidEmail.IsFalse)
    
    #EmailAddressEdit.ShowError( #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT.FAILED> )
    
    Else
    
    If (#AccountInUse)
    
    #EmailAddressEdit.ShowError( #STRINGS.Text<EMAILADDRESSEDIT.HELPERTEXT.EXISTS> )
    
    Else
    
    #NewAccount.EmailAdress := #EmailAddressEdit
    
    Endif
    
    Endif
    
    Endif
    
    * Password Entered
    If (#UserPasswordEdit.value.IsNull)
    
    #UserPasswordEdit.ShowError( #STRINGS.Text<USERPASSWORDEDIT.HELPERTEXT.BLANK> )
    
    Else
    
    If (#UserPasswordEdit.value.IsValidPassword( Medium ).IsFalse)
    
    #UserPasswordEdit.ShowError( #STRINGS.Text<USERPASSWORDEDIT.HELPERTEXT.WEAK> )
    
    Endif
    
    Endif
    
    If ((#UserPasswordEdit.HasError.IsFalse) *AndIf (#ConfirmPasswordEdit.Value.IsNull))
    
    #ConfirmPasswordEdit.ShowError( #STRINGS.Text<CONFIRMPASSWORDEDIT.HELPERTEXT.BLANK> )
    
    Else
    
    If ((#UserPasswordEdit.Value <> #ConfirmPasswordEdit.Value))
    
    #ConfirmPasswordEdit.ShowError( #STRINGS.Text<CONFIRMPASSWORDEDIT.HELPERTEXT.NOMATCH> )
    
    Endif
    
    Endif
    
    * Check if we have any errors
    For Each(#control) In(#COM_SELF.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.Edit)
    
    If (#control.HasError)
    
    #Result := False
    Return
    
    Endif
    
    Endfor
    
    Endroutine
    
    Mthroutine Name(ClearAllErrors) Access(*PRIVATE)
    
    * Check if we have any errors
    For Each(#Control) In(#COM_SELF.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.Edit)
    
    If (#Control.HasError)
    
    #Control.ClearError
    
    Endif
    
    Endfor
    
    Endroutine
    
    * Responsive Design
    
    Mthroutine Name(HandleDeviceChanged) Access(*PRIVATE)
    
    Case Of_Field(#SYS_WEB.Device)
    
    When Value_Is(= Mobile)
    
    #COM_SELF.SetForMobile
    
    When Value_Is(= Tablet)
    
    #COM_SELF.SetForTablet
    
    Otherwise
    
    #COM_SELF.SetForDesktop
    
    Endcase
    
    Endroutine
    
    Mthroutine Name(SetForMobile) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #MobileLayout)
    
    #COM_SELF.LayoutManager <= #MobileLayout
    
    Endif
    Endroutine
    
    Mthroutine Name(SetForTablet) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetForDesktop) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := True
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    End_Com
