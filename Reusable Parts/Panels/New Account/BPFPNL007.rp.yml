Reusable Part:
  Name:               BPFNewAccountFailedPanel
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Portal New Account Failed"

  - JPN:
      Description:        "Portal New Account Failed"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_View) Displayposition(1) Height(426) Tabposition(1) Width(666) Layoutmanager(#Layout)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFNewAccountFailedPanelStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#Prim_vs.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Panel Layouts
    
    * Base Layout
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(3) Parent(#Layout) Units(Content) Width(252)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(2) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#Layout) Height(48) Units(Pixels)
    
    * Detail Panel Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#DialogLogo) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem2) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#MainInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem3) Alignment(TopLeft) Column(#LayoutColumn1) Flow(Down) Manage(#SecondaryInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Marginbottom(24) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem10) Alignment(CenterRight) Column(#LayoutColumn3) Manage(#DetailsPanelNextButton) Parent(#Layout) Row(#LayoutRow2) Sizing(None)
    
    * Mobile Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem4) Manage(#PanelText) Parent(#Layout) Row(#LayoutRow1) Column(#LayoutColumn1) Alignment(TopLeft) Flow(Down) Columnspan(2)
    
    Define_Com Class(#PRIM_TBLO) Name(#MobileLayout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MobileLayoutColumn1) Displayposition(1) Parent(#MobileLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MobileLayoutRow1) Displayposition(1) Parent(#MobileLayout)
    
    * Details Panel Mobile Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem1) Manage(#MainInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#MobileLayoutColumn1) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem2) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecondaryInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem9) Alignment(BottomRight) Column(#MobileLayoutColumn1) Manage(#DetailsPanelNextButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem3) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#PanelText) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Margintop(16)
    
    
    Define_Com Class(#PRIM_MD.Label) Name(#DialogLogo) Displayposition(1) Left(414) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading3+ForegroundTheme') Top(0) Height(378) Width(252) Captionalignment(Center) Wordwrap(True) Iconalignment(AboveCaption) Icon('error_outline') Iconheight(118) Iconthemedrawstyle('ForegroundMediumPrimary') Captionmarginleft(14) Captionmarginright(14) Caption('Please try again')
    Define_Com Class(#PRIM_MD.Label) Name(#MainInstruction) Displayposition(2) Left(0) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('Heading2') Top(0) Height(27) Wordwrap(True) Width(414) Caption('Oops... Sorry smoething went wrong.')
    Define_Com Class(#PRIM_MD.Label) Name(#SecondaryInstruction) Displayposition(3) Left(0) Parent(#COM_OWNER) Tabposition(3) Height(21) Wordwrap(True) Width(414) Caption('Your account might not be created') Top(27)
    Define_Com Class(#PRIM_MD.RaisedButton) Name(#DetailsPanelNextButton) Caption('Try Again') Displayposition(4) Left(334) Parent(#COM_OWNER) Tabposition(4) Themedrawstyle('MediumTitle') Top(384) Width(80) Captionmarginleft(8) Captionmarginright(8) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelText) Caption('Looks like we encountered a problem when we tried to create your account. Sorry about that. It is best if you try to create the account again. If you encounter another error, please let us know and our team will help right away.') Displayposition(5) Left(0) Parent(#COM_OWNER) Tabposition(5) Top(72) Height(378) Width(414) Captionalignment(TopLeft) Wordwrap(True)
    
    
    * Parent
    Define_Com Class(#BPFCreateAccountDialog) Name(#Dialog) Reference(*DYNAMIC)
    
    * Reference to Data Object
    Define_Com Class(#BPFUserAccountClass) Name(#NewAccount) Reference(*DYNAMIC)
    
    * Non Visual Objects
    Define_Com Class(#PRIM_BOLN) Name(#AccountInUse)
    Define_Com Class(#PRIM_TIMR) Name(#AccountCheckDelay) Interval(250) Startup(Manual)
    
    * Rotuines
    
    * Events
    
    * Creation
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get reference to parent
    #Dialog <= #BPFPortal.CreateDialog
    #NewAccount <= #Dialog.NewAccount
    
    #COM_SELF.SetStrings
    
    #COM_SELF.HandleDeviceChanged
    
    * Handle Display
    #COM_SELF.HandleDisplay
    
    Endroutine
    
    * Webpage
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    #COM_SELF.HandleDeviceChanged
    
    Endroutine
    
    * Actions
    Evtroutine Handling(#DetailsPanelNextButton.Click)
    
    #Navigate.BackToTarget Target(INITIAL)
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #MAININSTRUCTION.CAPTION := #STRINGS.Text<MAININSTRUCTION.CAPTION>
    #SECONDARYINSTRUCTION.CAPTION := #STRINGS.Text<SECONDARYINSTRUCTION.CAPTION>
    #PANELTEXT.CAPTION := #STRINGS.Text<PANELTEXT.CAPTION>
    #DIALOGLOGO.CAPTION := #STRINGS.Text<DIALOGLOGO.CAPTION>
    #DETAILSPANELNEXTBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELNEXTBUTTON.CAPTION>
    
    Endroutine
    
    Mthroutine Name(HandleDisplay)
    
    Case Of_Field(#Navigate.ValueInQueryString)
    
    When Value_Is(= SENDERROR)
    
    * Send Email Error
    #COM_SELF.HandleEmailSendError
    
    When Value_Is(= USERERROR)
    
    * Duplicate Error
    #COM_SELF.HandleDuplcateUserError
    
    Otherwise
    
    * Creation Error
    #COM_SELF.HandleUserCreationError
    
    Endcase
    
    Endroutine
    
    * Error States
    Mthroutine Name(HandleEmailSendError) Access(*PRIVATE)
    
    #SecondaryInstruction := 'Your welcome email got stuck...'
    
    #PanelText := "Don't worry we have created your account but it cannot be activated as we cannot send your welcome email. Your details are safely stored in our system, so we will try and send your email again soon. If you dont recieve it in 24 hours, let us know at accounts@myportal.com.au. Sorry for hassle, but no portal is perfect."
    
    #DialogLogo.Icon := 'alarm'
    #DialogLogo.Caption := 'Just wait a bit longer'
    #DetailsPanelNextButton.Caption := 'Done'
    
    Endroutine
    
    Mthroutine Name(HandleUserCreationError)
    
    #SecondaryInstruction := 'There was an error creating your account'
    
    #PanelText := "While we always try really hard to ensure everything works all the time, sometimes things do go wrong. When creating your account we hit a snag and your account was not created. To be on the safe side we have removed your unsaved details so you can try again. If you still have an issue, please let us know at accounts@myportal.com.au. Sorry for hassle, but no portal is perfect."
    
    #DialogLogo.Icon := 'warning'
    #DialogLogo.Caption := 'Have another go, please...'
    #DetailsPanelNextButton.Caption := 'Try again'
    
    Endroutine
    
    Mthroutine Name(HandleDuplcateUserError)
    
    #SecondaryInstruction := 'Are you trying to pull a fast one?'
    
    #PanelText := "Before we start to save your detail, we did one last check to see if you already had an account. We think you already have an account, or had an account in the past. So it might be best to try a forget password. To be on the safe side we have removed your unsaved details so can do this. If you sure you have no account with myportal, please let us know at accounts@myportal.com.au. Sorry for hassle, but no portal is perfect."
    
    #DialogLogo.Icon := 'warning'
    #DialogLogo.Caption := 'You might want to try forget password'
    #DetailsPanelNextButton.Caption := 'Try Again'
    
    Endroutine
    
    * Responsive Design
    Mthroutine Name(HandleDeviceChanged) Access(*PRIVATE)
    
    Case Of_Field(#SYS_WEB.Device)
    
    When Value_Is(= Mobile)
    
    #COM_SELF.SetForMobile
    
    When Value_Is(= Tablet)
    
    #COM_SELF.SetForTablet
    
    Otherwise
    
    #COM_SELF.SetForDesktop
    
    Endcase
    
    Endroutine
    
    Mthroutine Name(SetForMobile) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #MobileLayout)
    
    #COM_SELF.LayoutManager <= #MobileLayout
    
    Endif
    Endroutine
    
    Mthroutine Name(SetForTablet) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetForDesktop) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := True
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    End_Com
