Reusable Part:
  Name:               BPFPasswordResetSecurityCheckPanel
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Portal Password Reset Security Check"

  - JPN:
      Description:        "Portal Password Reset Security Check"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_View) Displayposition(1) Height(426) Tabposition(1) Width(666) Layoutmanager(#Layout)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFPasswordResetSecurityCheckPanelStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#Prim_vs.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Panel Layouts
    
    * Base Layout
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(3) Parent(#Layout) Units(Content) Width(252)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(2) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#Layout) Height(48) Units(Pixels)
    
    * Detail Panel Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#DialogLogo) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem2) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#MainInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem3) Alignment(TopLeft) Column(#LayoutColumn1) Flow(Down) Manage(#SecondarInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Marginbottom(24) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem6) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#SecurityCheckAnswerEdit) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(8) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem9) Alignment(CenterLeft) Column(#LayoutColumn1) Manage(#DetailsPanelActionButton) Parent(#Layout) Row(#LayoutRow2) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem10) Alignment(CenterRight) Column(#LayoutColumn3) Manage(#DetailsPanelNextButton) Parent(#Layout) Row(#LayoutRow2) Sizing(None)
    
    * Mobile Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem4) Manage(#BodyText) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Column(#LayoutColumn1) Alignment(TopLeft) Flow(Down) Columnspan(2) Marginbottom(24)
    
    Define_Com Class(#PRIM_TBLO) Name(#MobileLayout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MobileLayoutColumn1) Displayposition(1) Parent(#MobileLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MobileLayoutRow1) Displayposition(1) Parent(#MobileLayout)
    
    * Details Panel Mobile Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem1) Manage(#MainInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#MobileLayoutColumn1) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem2) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecondarInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem3) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#BodyText) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem5) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecurityCheckAnswerEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem8) Alignment(BottomLeft) Column(#MobileLayoutColumn1) Manage(#DetailsPanelActionButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem9) Alignment(BottomRight) Column(#MobileLayoutColumn1) Manage(#DetailsPanelNextButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    
    Define_Com Class(#PRIM_MD.Label) Name(#DialogLogo) Caption('myPortal. We trust you, but it does not hurt to check') Displayposition(2) Left(414) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading3+ForegroundTheme') Top(0) Height(378) Width(252) Captionalignment(Center) Wordwrap(True) Iconalignment(AboveCaption) Icon('lock') Iconheight(118) Iconthemedrawstyle('ForegroundMediumPrimary') Captionmarginleft(14) Captionmarginright(14)
    Define_Com Class(#PRIM_MD.Label) Name(#MainInstruction) Displayposition(1) Left(0) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('Heading2') Top(0) Height(27) Wordwrap(True) Width(414) Caption('Verify your myPortal security credentials')
    Define_Com Class(#PRIM_MD.Label) Name(#SecondarInstruction) Caption('Please answer your myPortal security question') Displayposition(3) Left(0) Parent(#COM_OWNER) Tabposition(4) Height(21) Wordwrap(True) Width(414) Top(27)
    Define_Com Class(#PRIM_MD.Label) Name(#BodyText) Caption('Just getting your security question, not long now...') Displayposition(4) Left(0) Parent(#COM_OWNER) Tabposition(3) Top(72) Height(21) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Edit) Name(#SecurityCheckAnswerEdit) Displayposition(5) Left(0) Parent(#COM_OWNER) Tabposition(5) Top(125) Width(414) Componenttag('This will be your username') Caption('Your answer')
    Define_Com Class(#PRIM_MD.FlatButton) Name(#DetailsPanelActionButton) Displayposition(6) Left(0) Parent(#COM_OWNER) Tabposition(6) Themedrawstyle('ForegroundMediumPrimary') Top(384) Caption('Back') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.RaisedButton) Name(#DetailsPanelNextButton) Caption('Next') Displayposition(7) Left(334) Parent(#COM_OWNER) Tabposition(7) Themedrawstyle('MediumTitle') Top(384) Width(80) Style(#NavigationLinkStyle)
    
    * Parent
    Define_Com Class(#BPFPasswordResetDialog) Name(#Dialog) Reference(*DYNAMIC)
    
    * Reference to Data Object
    
    * Non Visual Objects
    
    * Rotuines
    
    * Events
    
    * Creation
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get reference to parent
    #Dialog <= #BPFPortal.PasswordResetDialog
    
    #COM_SELF.SetStrings
    
    #BodyText := #Dialog.UserQuestion
    
    #COM_SELF.HandleDeviceChanged
    
    * Set waiting and call server for question
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    * Webpage
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    #COM_SELF.HandleDeviceChanged
    
    Endroutine
    
    Evtroutine Handling(#Session.OnCheckSecurityDetailsCompleted)
    
    #Navigate.To( 'passwordresetnewpassword' )
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnCheckSecurityDetailsFailed) Reason(#Reason)
    
    If (#Reason = NOMATCH)
    
    #SecurityCheckAnswerEdit.ShowError( #STRINGS.Text<SESSION.ONCHECKSECURITYDETAILSFAILED> )
    
    Else
    
    #Navigate.To( 'passwordresetcheckfailed' ADD True 'check' )
    
    Endif
    
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    * Edit Fields
    Evtroutine Handling(#SecurityCheckAnswerEdit.Changed)
    
    If (#SecurityCheckAnswerEdit.HasError)
    
    #SecurityCheckAnswerEdit.ClearError
    
    Endif
    
    Endroutine
    
    * Actions
    Evtroutine Handling(#DetailsPanelActionButton.Click)
    
    If (#BPFPortal.LoginDialog *IsNot *Null)
    
    * Set Address Values
    #SYS_WEB.History.GoBack
    
    Else
    
    #Navigate.BackToTarget Target(INITIAL)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#DetailsPanelNextButton.Click #SecurityCheckAnswerEdit.Enter)
    
    If (#SecurityCheckAnswerEdit.HasError.IsFalse)
    
    #Dialog.SetWaitingState( ON )
    #Session.CheckSecuirtyDetails( #SecurityCheckAnswerEdit )
    
    Endif
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #MAININSTRUCTION.CAPTION := #STRINGS.Text<MAININSTRUCTION.CAPTION>
    #SECONDARINSTRUCTION.CAPTION := #STRINGS.Text<SECONDARINSTRUCTION.CAPTION>
    #BODYTEXT.CAPTION := #STRINGS.Text<BODYTEXT.CAPTION>
    #SECURITYCHECKANSWEREDIT.CAPTION := #STRINGS.Text<SECURITYCHECKANSWEREDIT.CAPTION>
    #DIALOGLOGO.CAPTION := #STRINGS.Text<DIALOGLOGO.CAPTION>
    #DETAILSPANELACTIONBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELACTIONBUTTON.CAPTION>
    #DETAILSPANELNEXTBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELNEXTBUTTON.CAPTION>
    
    Endroutine
    
    * Responsive Design
    
    Mthroutine Name(HandleDeviceChanged) Access(*PRIVATE)
    
    Case Of_Field(#SYS_WEB.Device)
    
    When Value_Is(= Mobile)
    
    #COM_SELF.SetForMobile
    
    When Value_Is(= Tablet)
    
    #COM_SELF.SetForTablet
    
    Otherwise
    
    #COM_SELF.SetForDesktop
    
    Endcase
    
    Endroutine
    
    Mthroutine Name(SetForMobile) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #MobileLayout)
    
    #COM_SELF.LayoutManager <= #MobileLayout
    
    Endif
    Endroutine
    
    Mthroutine Name(SetForTablet) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetForDesktop) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := True
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    End_Com
