Reusable Part:
  Name:               BPFForgetPasswordRequestPanel
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Portal Forget Password Request"

  - JPN:
      Description:        "Portal Forget Password Request"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_View) Displayposition(1) Height(426) Tabposition(1) Width(666) Layoutmanager(#Layout)
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFForgetPasswordRequestPanelStrings) Name(#Strings)
    
    * Styles
    Define_Com Class(#Prim_vs.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    
    * Panel Layouts
    
    * Base Layout
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn2) Displayposition(3) Parent(#Layout) Units(Content) Width(252)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn3) Displayposition(2) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow2) Displayposition(2) Parent(#Layout) Height(48) Units(Pixels)
    
    * Detail Panel Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(TopLeft) Column(#LayoutColumn2) Manage(#DialogLogo) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem2) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#MainInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem3) Alignment(TopLeft) Column(#LayoutColumn1) Flow(Down) Manage(#SecondarInstruction) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Marginbottom(24) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem6) Alignment(TopLeft) Column(#LayoutColumn1) Manage(#ConfirmEmailEdit) Parent(#Layout) Row(#LayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(8) Columnspan(2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem9) Alignment(CenterLeft) Column(#LayoutColumn1) Manage(#DetailsPanelActionButton) Parent(#Layout) Row(#LayoutRow2) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem10) Alignment(CenterRight) Column(#LayoutColumn3) Manage(#DetailsPanelNextButton) Parent(#Layout) Row(#LayoutRow2) Sizing(None)
    
    * Mobile Layout
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem4) Manage(#BodyText) Parent(#Layout) Row(#LayoutRow1) Sizing(ContentHeightFitToWidth) Column(#LayoutColumn1) Alignment(TopLeft) Flow(Down) Columnspan(2) Marginbottom(24)
    
    Define_Com Class(#PRIM_TBLO) Name(#MobileLayout)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MobileLayoutColumn1) Displayposition(1) Parent(#MobileLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MobileLayoutRow1) Displayposition(1) Parent(#MobileLayout)
    
    * Details Panel Mobile Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem1) Manage(#MainInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Column(#MobileLayoutColumn1) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem2) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#SecondarInstruction) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem3) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#BodyText) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(ContentHeightFitToWidth) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem5) Alignment(TopLeft) Column(#MobileLayoutColumn1) Flow(Down) Manage(#ConfirmEmailEdit) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(FitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem8) Alignment(BottomLeft) Column(#MobileLayoutColumn1) Manage(#DetailsPanelActionButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MobileLayoutItem9) Alignment(BottomRight) Column(#MobileLayoutColumn1) Manage(#DetailsPanelNextButton) Parent(#MobileLayout) Row(#MobileLayoutRow1) Sizing(None) Marginbottom(24)
    
    Define_Com Class(#PRIM_MD.Label) Name(#DialogLogo) Caption('myPortal. Email help is on it way, hold tight') Displayposition(2) Left(414) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading3+ForegroundTheme') Top(0) Height(378) Width(252) Captionalignment(Center) Wordwrap(True) Iconalignment(AboveCaption) Icon('email') Iconheight(118) Iconthemedrawstyle('ForegroundMediumPrimary') Captionmarginleft(14) Captionmarginright(14)
    Define_Com Class(#PRIM_MD.Label) Name(#MainInstruction) Caption('Recover your myPortal account password') Displayposition(1) Left(0) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('Heading2') Top(0) Height(27) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Label) Name(#SecondarInstruction) Caption('Confirm the email address of your account') Displayposition(3) Left(0) Parent(#COM_OWNER) Tabposition(4) Top(27) Height(21) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Label) Name(#BodyText) Caption('Once you confirm your email address we will send you a message with the steps to follow to recover your password. If you have any issues just lets use now at accounts@myPortal.com.au') Displayposition(4) Left(0) Parent(#COM_OWNER) Tabposition(3) Top(72) Height(55) Wordwrap(True) Width(414)
    Define_Com Class(#PRIM_MD.Edit) Name(#ConfirmEmailEdit) Displayposition(5) Left(0) Parent(#COM_OWNER) Tabposition(5) Top(159) Width(414) Helpertext('If this isnt right just change it') Componenttag('This will be your username') Caption('Your email')
    Define_Com Class(#PRIM_MD.FlatButton) Name(#DetailsPanelActionButton) Displayposition(6) Left(0) Parent(#COM_OWNER) Tabposition(6) Themedrawstyle('ForegroundMediumPrimary') Top(384) Caption('Back') Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.RaisedButton) Name(#DetailsPanelNextButton) Caption('Confirm') Displayposition(7) Left(334) Parent(#COM_OWNER) Tabposition(7) Themedrawstyle('MediumTitle') Top(384) Width(80) Style(#NavigationLinkStyle)
    
    * Parent
    Define_Com Class(#BPFForgetPasswordRequestDialog) Name(#Dialog) Reference(*DYNAMIC)
    
    * Reference to Data Object
    
    * Non Visual Objects
    
    * Rotuines
    
    * Events
    
    * Creation
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Get reference to parent
    #Dialog <= #BPFPortal.ForgetPasswordDialog
    
    * Set Strings
    #COM_SELF.SetStrings
    
    * Set Address Values
    #ConfirmEmailEdit := #Session.RequestedAccount
    
    #COM_SELF.HandleDeviceChanged
    
    Endroutine
    
    * Webpage
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    #COM_SELF.HandleDeviceChanged
    
    Endroutine
    
    * Edit Fields
    Evtroutine Handling(#ConfirmEmailEdit.Changed)
    
    If (#ConfirmEmailEdit.Value.IsValidEmail)
    
    #ConfirmEmailEdit.ClearError
    
    Else
    
    #ConfirmEmailEdit.ShowError( #STRINGS.Text<CONFIRMEMAILEDIT.CHANGED.INVALID> )
    
    Endif
    
    Endroutine
    
    * Actions
    Evtroutine Handling(#DetailsPanelActionButton.Click)
    
    * If (#BPFPortal.LoginDialog *IsNot *Null)
    If (*Not #Dialog.OpenedFromURL)
    
    * Set Address Values
    #Navigate.BackToTarget( INITIAL )
    
    Else
    
    #Navigate.To Eventname(*Null) Type(REPLACE) Signalreplaceevent(True)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#DetailsPanelNextButton.Click #ConfirmEmailEdit.Enter)
    
    If (#ConfirmEmailEdit.HasError.IsFalse)
    
    #Dialog.SetWaitingState( ON )
    #Session.RequestPasswordReset( #ConfirmEmailEdit )
    
    Endif
    
    Endroutine
    
    * Session
    Evtroutine Handling(#Session.OnPasswordResetRequestCompleted)
    
    #Navigate.To Eventname(requestpasswordcompleted) Signalreplaceevent(True) Type(REPLACE)
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    Evtroutine Handling(#Session.OnPasswordResetRequestFailed)
    
    #ConfirmEmailEdit.ShowError( #STRINGS.Text<SESSION.ONPASSWORDRESETREQUESTFAILED> )
    #Dialog.SetWaitingState( OFF )
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #MAININSTRUCTION.CAPTION := #STRINGS.Text<MAININSTRUCTION.CAPTION>
    #SECONDARINSTRUCTION.CAPTION := #STRINGS.Text<SECONDARINSTRUCTION.CAPTION>
    #BODYTEXT.CAPTION := #STRINGS.Text<BODYTEXT.CAPTION>
    #DIALOGLOGO.CAPTION := #STRINGS.Text<DIALOGLOGO.CAPTION>
    #CONFIRMEMAILEDIT.CAPTION := #STRINGS.Text<CONFIRMEMAILEDIT.CAPTION>
    #CONFIRMEMAILEDIT.HELPERTEXT := #STRINGS.Text<CONFIRMEMAILEDIT.HELPERTEXT>
    #DETAILSPANELACTIONBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELACTIONBUTTON.CAPTION>
    #DETAILSPANELNEXTBUTTON.CAPTION := #STRINGS.Text<DETAILSPANELNEXTBUTTON.CAPTION>
    
    Endroutine
    
    * Responsive Design
    
    Mthroutine Name(HandleDeviceChanged) Access(*PRIVATE)
    
    Case Of_Field(#SYS_WEB.Device)
    
    When Value_Is(= Mobile)
    
    #COM_SELF.SetForMobile
    
    When Value_Is(= Tablet)
    
    #COM_SELF.SetForTablet
    
    Otherwise
    
    #COM_SELF.SetForDesktop
    
    Endcase
    
    Endroutine
    
    Mthroutine Name(SetForMobile) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #MobileLayout)
    
    #COM_SELF.LayoutManager <= #MobileLayout
    
    Endif
    Endroutine
    
    Mthroutine Name(SetForTablet) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := False
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetForDesktop) Access(*PRIVATE)
    
    * Set Logo Visibility
    #DialogLogo.Visible := True
    
    * Set Layout Manager and Min Content Panel Height
    If (#COM_SELF.LayoutManager *IsNotEqualTo #Layout)
    
    #COM_SELF.LayoutManager <= #Layout
    
    Endif
    
    Endroutine
    
    End_Com
