Reusable Part:
  Name:               BPFClientLanguageStrings
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Client Language Strings Resource"

  - JPN:
      Description:        "Client Language Strings Resource"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_OBJT)
    
    * Application References
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFBrowserEvents) Name(#Browser) Scope(*APPLICATION)
    
    * Poperties
    Define_Pty Name(Strings) Get(*REFERENCE #StringsCollection)
    Define_Pty Name(Text) Get(GetText)
    
    Define_Com Class(#Prim_dc.UnicodeString) Name(#MyContext)
    
    * String Collection
    Define_Com Class(#PRIM_KCOL<#PRIM_DC.UnicodeString #BPFGenericNVarChar256>) Name(#StringsCollection)
    
    Ptyroutine Name(GetText) Options(*FINAL)
    Define_Map For(*OUTPUT) Class(#PRIM_DC.UnicodeString) Name(#Property)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Key)
    
    * Check the specific language first, and if null fall back to generic
    If (#StringsCollection<#Key>.IsNull)
    
    * Return default test
    #Property := 'No matching string found.'
    
    Else
    
    * Set String
    #Property := #StringsCollection<#Key>
    
    Endif
    
    Endroutine
    
    * Events
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    #COM_SELF.GetStringData
    
    Endroutine
    
    
    * Methods
    
    * Private
    
    Mthroutine Name(GetStringData) Options(*FINAL) Access(*PRIVATE)
    
    Define_Map For(*RESULT) Class(#PRIM_BOLN) Name(#Result) Mandatory(False)
    
    * Define Request to Server Routine
    Define_Com Class(#prim_web.HttpRequest) Name(#Request)
    Define_Com Class(#prim_web.json) Name(#JSON)
    
    * Make Request for language option json for object
    #Request.URL := 'content/' + #com_self.ComponentClassName.lowercase + '.json?plv=' + #Browser.LanguageVersion
    #Request.Execute
    
    Evtroutine Handling(#Request.Completed)
    
    If (#Request.Response.StatusCode = 200)
    
    * Get the returned result and place it into a local JSON object and pass it for processing
    #JSON := #Request.Response.Content
    #COM_SELF.BuildCollection( (#JSON.RootItem<'ls'> *As #prim_web.JsonArray) )
    #Result := True
    
    Endif
    
    Endroutine
    
    Endroutine
    
    * This is common to all language resources, to process returned languages
    Mthroutine Name(BuildCollection) Options(*FINAL) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#prim_web.JsonArray) Name(#StringsArray) Pass(*BY_REFERENCE)
    
    * Work through the returned strings
    For Each(#ObjectElement) In(#StringsArray)
    
    * Override default value in the array with the language as the key suffix
    #StringsCollection<(#ObjectElement<'op'>.AsString)> := #ObjectElement<'s'>.AsString
    
    Endfor
    
    Endroutine
    
    End_Com
