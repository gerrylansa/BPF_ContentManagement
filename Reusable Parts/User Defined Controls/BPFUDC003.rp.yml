Reusable Part:
  Name:               BPFCountrySelectionControl
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Multilingual Details:
  - ENG:
      Description:        "Drop down selection of country"

  - JPN:
      Description:        "Drop down selection of country"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_PANL) Displayposition(1) Left(0) Tabposition(1) Top(0) Layoutmanager(#Layout) Width(220) Height(75)
    
    * Language Support
    Define_Com Class(#BPFClientCountryCodeStrings) Name(#Strings)
    
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#LayoutColumn1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#LayoutRow1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(TopCenter) Column(#LayoutColumn1) Manage(#DropDown) Parent(#Layout) Row(#LayoutRow1)
    
    Define_Evt Name(Changed)
    
    Define_Pty Name(Control) Get(*REFERENCE #DropDown)
    Define_Com Class(#PRIM_MD.Dropdown) Name(#DropDown) Caption('Country') Displayposition(1) Left(0) Parent(#COM_OWNER) Tabposition(1) Top(0) Width(220) Promptonly(False)
    
    Define_Com Class(#PRIM_TIMR) Name(#SignalDelay) Startup(Manual) Interval(100)
    
    * Events
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Handle Creation
    #COM_SELF.HandleCreation
    
    Endroutine
    
    Evtroutine Handling(#DropDown.Changed)
    
    #SignalDelay.Stop
    
    #DropDown.OpenDropDown
    
    #SignalDelay.Start
    
    Endroutine
    
    Evtroutine Handling(#SignalDelay.Tick)
    
    #SignalDelay.Stop
    Signal Event(Changed)
    
    Endroutine
    
    * Methods
    Mthroutine Name(HandleCreation)
    
    * Create Countries via Strings Collection
    For Each(#String) In(#Strings.Strings) Key(#STD_CODE)
    
    #DropDown.Items.Add( #STD_CODE #String )
    
    Endfor
    
    Endroutine
    
    End_Com
