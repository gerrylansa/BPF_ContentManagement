Web Dialog:
  Name:               BPFShareItemConfirmRemoveDialog
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Share Item Confirm Remove Dialog"

  - JPN:
      Description:        "Share Item Confirm Remove Dialog"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_DLG) Width(392) Height(180) Layoutmanager(#Layout1) Style(#Style1) Themedrawstyle('text(0,0,0,0.87)')
    
    * Application References
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * Strings
    Define_Com Class(#BPFShareItemConfirmRemoveDialogStrings) Name(#Strings)
    
    * Style
    Define_Com Class(#PRIM_VS.Style) Name(#Style1) Cornerbottomleft(4) Cornerbottomright(4) Cornertopleft(4) Cornertopright(4) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    
    * Dialog Layout
    Define_Com Class(#PRIM_TBLO) Name(#Layout1) Marginbottom(24) Marginleft(24) Marginright(24) Margintop(24) Sizing(ContentHeight)
    
    * Rows & Columns
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row1) Displayposition(1) Parent(#Layout1) Height(26) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Layout1Column1) Displayposition(1) Parent(#Layout1)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row2) Displayposition(2) Parent(#Layout1) Minheight(72) Height(41) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Layout1Row3) Displayposition(3) Parent(#Layout1) Height(32) Units(Pixels)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#Layout1Item1) Alignment(TopLeft) Column(#Layout1Column1) Manage(#DialogTitle) Parent(#Layout1) Row(#Layout1Row1) Sizing(ContentWidthFitToHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#Layout1Item2) Alignment(TopRight) Column(#Layout1Column1) Manage(#DialogClose) Parent(#Layout1) Row(#Layout1Row1) Sizing(ContentWidthAndHeight)
    Define_Com Class(#PRIM_TBLO.Item) Name(#Layout1Item3) Alignment(TopRight) Column(#Layout1Column1) Manage(#DialogConfirm) Parent(#Layout1) Row(#Layout1Row3) Sizing(ContentWidthFitToHeight) Flow(Left)
    Define_Com Class(#PRIM_TBLO.Item) Name(#Layout1Item4) Alignment(TopRight) Column(#Layout1Column1) Manage(#DialogCancel) Parent(#Layout1) Row(#Layout1Row3) Sizing(ContentWidthFitToHeight) Flow(Left) Marginright(24)
    
    * Controls
    Define_Com Class(#PRIM_TBLO.Item) Name(#Layout1Item5) Alignment(CenterLeft) Column(#Layout1Column1) Manage(#MessageText) Parent(#Layout1) Row(#Layout1Row2) Sizing(ContentHeightFitToWidth)
    
    Define_Com Class(#PRIM_MD.Label) Name(#DialogTitle) Caption('Remove') Displayposition(1) Left(24) Parent(#COM_OWNER) Tabposition(1) Themedrawstyle('Heading2') Top(24) Width(73) Height(26)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#DialogClose) Displayposition(2) Left(342) Parent(#COM_OWNER) Tabposition(2) Top(24) Width(24) Height(24) Icon('close') Iconheight(16) Paddingbottom(4) Paddingleft(4) Paddingright(4) Paddingtop(4)
    Define_Com Class(#PRIM_MD.RaisedButton) Name(#DialogConfirm) Caption('REMOVE') Displayposition(3) Left(286) Parent(#COM_OWNER) Tabposition(3) Themedrawstyle('MediumTitle') Top(122) Width(80) Height(32) Paddingleft(10) Paddingright(10)
    Define_Com Class(#PRIM_MD.FlatButton) Name(#DialogCancel) Caption('CANCEL') Displayposition(4) Left(185) Parent(#COM_OWNER) Tabposition(4) Top(122) Width(77) Height(32) Paddingleft(10) Paddingright(10)
    Define_Com Class(#PRIM_MD.Label) Name(#MessageText) Caption('Are you sure you wish to remove &1 from the share') Displayposition(5) Height(41) Left(24) Parent(#COM_OWNER) Tabposition(5) Top(66) Width(342) Wordwrap(True) Captionmarginbottom(12) Captionmargintop(12)
    
    * References
    Define_Com Class(#PRIM_LCOL<#BPFContentShareItemClass>) Name(#SelectedItems) Reference(*DYNAMIC)
    
    * Routines
    
    * Events
    Define_Evt Name(OnActionConfirmed)
    
    * Self
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    #COM_SELF.SetStrings
    #COM_SELF.SetTheme
    
    Endroutine
    
    Evtroutine Handling(#BPFPortal.SizeChanged #COM_OWNER.Prepare)
    
    #COM_SELF.HandleSetWidth
    
    Endroutine
    
    * Navigate
    Evtroutine Handling(#Navigate.OnNavigationEvent)
    
    #COM_SELF.CloseDialog
    
    Endroutine
    
    * Actions
    Evtroutine Handling(#DialogCancel.Click #DialogClose.Click)
    
    #COM_SELF.CloseDialog
    
    Endroutine
    
    Evtroutine Handling(#DialogConfirm.Click)
    
    Signal Event(OnActionConfirmed)
    #COM_SELF.CloseDialog
    
    Endroutine
    
    * Methods
    Mthroutine Name(OpenDialog)
    Define_Map For(*INPUT) Class(#PRIM_LCOL<#BPFContentShareItemClass>) Name(#Items) Mandatory(*NULL) Pass(*BY_REFERENCE)
    
    * Set Reference
    #SelectedItems <= #Items
    
    * Set Message
    #COM_SELF.SetMessage
    
    * Open the dialog
    #COM_SELF.Show
    
    Endroutine
    
    Mthroutine Name(CloseDialog)
    
    * Clear References
    #SelectedItems <= *Null
    
    * Close
    #COM_SELF.Close
    
    Endroutine
    
    Mthroutine Name(SetMessage) Access(*PRIVATE)
    
    * 1 Item only - Item 1 Share Item Name "Jay Lenos Garage - Holden Commodore"
    * 1 Folder Only - "Folder 10" and all the items inside
    * All Items - these 5 items
    * Mixed - these 5 items and the items inside any folders
    
    If (#SelectedItems *Is *Null)
    
    #MessageText.Caption := #STRINGS.Text<'MESSAGETEXT.CAPTION.SHARE'>
    
    Else
    
    If (#SelectedItems.ItemCount = 1)
    
    If (#SelectedItems.item<1>.IsFolder)
    
    #MessageText.Caption := #STRINGS.TEXT<'MESSAGETEXT.CAPTION.FOLDER'>.replace( '%%FOLDER%%' #SelectedItems.item<1>.ItemName )
    
    Else
    
    #MessageText.Caption := #STRINGS.TEXT<'MESSAGETEXT.CAPTION.ITEM'>.replace( '%%ITEM%%' #SelectedItems.item<1>.ItemName )
    
    Endif
    
    Else
    
    If (#COM_SELF.CheckForFolder)
    
    #MessageText.Caption := #STRINGS.TEXT<'MESSAGETEXT.CAPTION.FOLDERS'>.replace( '%%COUNT%%' #SelectedItems.ItemCount.AsString )
    
    Else
    
    #MessageText.Caption := #STRINGS.TEXT<'MESSAGETEXT.CAPTION.ITEMS'>.replace( '%%COUNT%%' #SelectedItems.ItemCount.AsString )
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(CheckForFolder)
    Define_Map For(*RESULT) Class(#PRIM_BOLN) Name(#Result)
    
    For Each(#Item) In(#SelectedItems)
    
    #Result := #Item.IsFolder
    Leave If(#Result)
    
    Endfor
    
    Endroutine
    
    Mthroutine Name(HandleSetWidth) Access(*PRIVATE)
    
    If (#SYS_WEB.WebPage.Width < 400)
    
    #COM_SELF.Width := #SYS_WEB.WebPage.Width - 32
    
    Else
    
    #COM_SELF.Width := 392
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetStrings)
    
    #DIALOGTITLE.CAPTION := #STRINGS.Text<'DIALOGTITLE.CAPTION'>
    #DIALOGCANCEL.CAPTION := #STRINGS.Text<'DIALOGCANCEL.CAPTION'>
    #DIALOGCONFIRM.CAPTION := #STRINGS.Text<'DIALOGCONFIRM.CAPTION'>
    
    Endroutine
    
    Mthroutine Name(SetTheme)
    
    If (#SYS_WEB.WebPage.ThemeBackground = Light)
    
    #COM_SELF.ThemeDrawStyle := 'text(0,0,0,0.87)'
    
    Else
    
    #COM_SELF.ThemeDrawStyle := 'text(255,255,255,0.87)'
    
    Endif
    
    Endroutine
    
    End_Com
